# Money

> Module Money_en

> Using Money_internal as M

## Helper functions

```catala-metadata
## Returns the smaller of the two arguments.
declaration min
  content money
  depends on
    m1 content money,
    m2 content money
  equals
    if m1 > m2 then m2 else m1

## Returns the bigger of the two arguments.
declaration max
  content money
  depends on
    m1 content money,
    m2 content money
  equals
    if m1 > m2 then m1 else m2

## Caps `m`, returning a value that never exceeds `max_value`.
declaration ceiling
  content money
  depends on
    m content money,
    max_value content money
  equals
    min of m, max_value

## Puts a lower cap on `m`, and returns a value that is never smaller
## than `min_value`.
declaration floor
  content money
  depends on
    m content money,
    min_value content money
  equals
    max of m, min_value

## Returns the argument if it is positive, $0 otherwise.
declaration positive
  content money
  depends on m content money
  equals
    floor of m, $0

## Removes decimal digits from a money amount.
## **Examples:**
## - `truncate of $7.61 = $7.0`
## - `truncate of -$7.61 = -$7.0`
declaration truncate
  content money
  depends on m content money
  equals
    if m = $0 then $0
    else if m > $0 then
      round of (m - $0.5)
    else
      round of (m + $0.5)

## Rounds a money amount to the next greater dollar.
## **Examples:**
## - `round_by_excess of $4.34 = $5`
## - `round_by_excess of -$4.34 = -$4.0`
declaration round_by_excess
  content money
  depends on m content money
  equals
    if m >= $0 then
      round of (m + $0.49)
    else
      round of (m + $0.5)

## Rounds a money amount to the previous lesser dollar.
## **Examples:**
## - `round_by_default of $3.78 = $3`
## - `round_by_default of -$3.78 = -$4.0`
declaration round_by_default
  content money
  depends on m content money
  equals
    if m > $0 then
      round of (m - $0.5)
    else
      round of (m - $0.49)

## Rounds a money amount to the specified `nth_decimal`.
## `nth_decimal` may be a negative value.
## **Examples:**
## - `round_to_decimal of $123.45, 1 = $123.5`
## - `round_to_decimal of $123.45, -2 = $100`
declaration round_to_decimal
  content money
  depends on
    m content money,
    nth_decimal content integer
  equals
    M.round_to_decimal of m, nth_decimal

## Sums the elements of a list of money
declaration sum
  content money
  depends on l content list of money
  equals combine all x among l in total initially $0 with total + x

```

## Financial operations

```catala-metadata
## Returns the positive amount that `m` overflows from `reference`
## ($0 otherwise).
declaration in_excess
  content money
  depends on
    m content money,
    reference content money
  equals
    max of ($0, m - reference)

## Returns the positive amount that `m` underflows from `reference`
## ($0 otherwise).
declaration in_default
  content money
  depends on
    m content money,
    reference content money
  equals
    max of ($0, reference - m)

```
