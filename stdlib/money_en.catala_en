# Money

> Module Money_en

## Helper functions

```catala-metadata
## Returns the smallest of the two arguments.
declaration min
  content money
  depends on m1 content money,
    m2 content money
  equals
    if m1 > m2 then m2 else m1

## Returns the biggest of the two arguments.
declaration max
  content money
  depends on m1 content money,
    m2 content money
  equals
    if m1 > m2 then m1 else m2

## `ceiling of variable, max_value` puts an upper cap to `variable` and returns
## a value that never exceeds `max_value`.
declaration ceiling
  content money
  depends on variable content money,
    max_value content money
  equals min of variable, max_value

## `floor of variable, min_value` puts a lower cap to `variable` and returns
## a value that is never inferior to `min_value`.
declaration floor
  content money
  depends on variable content money,
    min_value content money
  equals max of variable, min_value

## Returns the argument if it is positive, $0 otherwise.
declaration positive
  content money
  depends on variable content money
  equals floor of variable, $0

## Removes decimal digits from a money amount. For instance,
## `truncate of $7.61 = $7.0` and `truncate of -$7.61 = -$7.0`.
declaration truncate
  content money
  depends on variable content money
  equals
    if variable = $0 then $0
    else if variable > $0 then
      round of (variable - $0.5)
    else round of (variable + $0.5)

## Rounds a money amount to the next greater dollar. For instance,
## `round_by_excess of $4.34 = $5` and `round_by_excess of -$4.34 = -$4.0`.
declaration round_by_excess
  content money
  depends on variable content money
  equals
    if variable >= $0 then
      round of (variable + $0.49)
    else round of (variable + $0.5)

## Rounds a money amount to the previous lesser dollar. For instance,
## `round_by_excess of $3.78 = $3` and `round_by_excess of -$3.78 = -$4.0`.
declaration round_by_default
  content money
  depends on variable content money
  equals
    if variable > $0 then
      round of (variable - $0.5)
    else round of (variable - $0.49)
```

## Financial operations

```catala-metadata
## Returns the positive amount that `variable` overflows from `reference`
## ($0 otherwise).
declaration in_excess
  content money
  depends on variable content money,
    reference content money
  equals max of ($0, variable - reference)

## Returns the positive amount that `variable` underflows from `reference`
## ($0 otherwise).
declaration in_default
  content money
  depends on variable content money,
    reference content money
  equals max of ($0, reference - variable)
```
