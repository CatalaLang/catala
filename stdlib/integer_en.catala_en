# Integers

> Module Integer_en

## Helper functions

```catala-metadata
## Returns the smallest of the two arguments.
declaration min
  content integer
  depends on
    x content integer,
    y content integer
  equals
    if x <= y then x else y

## Returns the biggest of the two arguments.
declaration max
  content integer
  depends on
    x content integer,
    y content integer
  equals
    if x >= y then x else y

## `ceiling of variable, max_value` puts an upper cap to `variable` and returns
## a value that never exceeds `max_value`.
declaration ceiling
  content integer
  depends on
    variable content integer,
    max_value content integer
  equals
    min of variable, max_value

## `floor of variable, min_value` puts a lower cap to `variable` and returns
## a value that is never inferior to `min_value`.
declaration floor
  content integer
  depends on
    variable content integer,
    min_value content integer
  equals
    max of variable, min_value

## Returns the argument if it is positive, 0 otherwise.
declaration positive
  content integer
  depends on variable content integer
  equals
    floor of variable, 0
```
