# Lists

> Module List_en
> Using List_internal

## Helper functions

```catala-metadata
## Returns a list made of the consecutive integers from `begin` (inclusive)
## to `end` (exclusive).
## **Example**: `sequence of 3, 6` is the list `[3; 4; 5]`
## **Edge-case**: if `end <= begin`, the returned list is empty
declaration sequence
  content list of integer
  depends on begin content integer,
             end content integer
  equals
  List_internal.sequence of begin, end

## Returns the element of the list at the given index, encapsulated within
## constructor `PrÃ©sent`
## **Example**: `nth_element of [$101; $102; $103], 2` is `Present content $102`
## **Edge-case**: if the index is less than 1, or otherwise outside of the list,
## `Absent` is returned
declaration nth_element
  content optional of anything of type t
  depends on lst content list of anything of type t,
             index content integer
  equals
  List_internal.nth_element of lst, index

## Returns the first element of the list, encapsulated within constructor
## `Present`.
## **Edge-case**: if the list is empty, returns `Absent` instead
declaration first_element
  content optional of anything of type t
  depends on lst content list of anything of type t
  equals
  nth_element of lst, 1

## Returns the last element of the list, encapsulated within constructor
## `Present`.
## **Edge-case**: if the list is empty, returns `Absent`
declaration last_element
  content optional of anything of type t
  depends on lst content list of anything of type t
  equals
  nth_element of lst, (number of lst)

## Removes the element at `index` within the list. The indexes of the
## following elements are shifted.
## **Edge-case**: if the index is invalid, the list is returned unchanged
declaration remove_nth_element
  content list of anything of type t
  depends on lst content list of anything of type t,
             index content integer
  equals
  List_internal.remove_nth_element of lst, index

## Returns the given list, without its first element.
## **Edge-case**: an empty list is returned unchanged
declaration remove_first_element
  content list of anything of type t
  depends on lst content list of anything of type t
  equals
  List_internal.remove_nth_element of lst, 1

## Returns the given list, without its last element.
## **Edge-case**: an empty list is returned unchanged
declaration remove_last_element
  content list of anything of type t
  depends on lst content list of anything of type t
  equals
  List_internal.remove_nth_element of lst, (number of lst)
```
