@@Section 1015@@

@@Begin metadata@@
/*

declaration enumeration AcquisitionMethod:
  -- Gift
  -- Trust

declaration structure Acquisition:
  data method content AcquisitionMethod
  data moment content date
  data no_sale_or_exchange_before content boolean

declaration structure Value:
  data fair_market content amount depends on date
  data last_acquisition content amount
  data net_appreciation content amount

declaration structure Transferor:
  data basis content amount
  data basis_known content optional amount
  data acquisition content Acquisition
  data gain_or_loss content amount depends on date

declaration scope BasisOfGift:
  context acquisition content Acquisition
  context basis_subsection_a content amount
  context basis content amount
  context basis_bonus_after_1976 content amount
  context transferor content Transferor
  context gift_tax_paid content amount
*/
@@End metadata@@

@(a) Gifts after December 31, 1920@
If the property was acquired by gift after December 31, 1920, the basis shall be 
the same as it would be in the hands of the donor or the last preceding owner by 
whom it was not acquired by gift, except that if such basis (adjusted for the 
period before the date of the gift as provided in section 1016) is greater than 
the fair market value of the property at the time of the gift, then for the 
purpose of determining loss the basis shall be such fair market value.
/*
scope BasisOfGift:
  definition basis_subsection_a equals
    if transferor.basis >= value.fair_market of acquisition.moment then
      value.fair_market of acquisition.moment
    else
      transferor.basis

  definition basis under condition
    acquisition.moment > |01/31/1920| and
    acquisition.method = Gift
  consequence equals basis_subsection_a

*/
 If the facts necessary to determine the basis in the hands of the donor or the 
 last preceding owner are unknown to the donee, the Secretary shall, if 
 possible, obtain such facts from such donor or last preceding owner, or any 
 other person cognizant thereof. If the Secretary finds it impossible to obtain 
 such facts, the basis in the hands of such donor or last preceding owner shall 
 be the fair market value of such property as found by the Secretary as of the 
 date or approximate date at which, according to the best information that the 
 Secretary is able to obtain, such property was acquired by such donor or last 
 preceding owner.
/*
scope BasisOfGift under condition
  acquisition.moment > |01/31/1920| and
  acquisition.method = Gift:

  definition transferor.basis equals
    match transferor.known_basis with pattern
    -- Some of basis : basis
    -- None : value.fair_market of transferor.acquisition.moment
*/


@(b) Transfer in trust after December 31, 1920@
If the property was acquired after December 31, 1920, by a transfer in trust 
(other than by a transfer in trust by a gift, bequest, or devise), the basis 
shall be the same as it would be in the hands of the grantor increased in the 
amount of gain or decreased in the amount of loss recognized to the grantor on 
such transfer under the law applicable to the year in which the transfer was 
made.
/*
scope BasisOfGift under condition
  acquisition.moment > |01/31/1920| and
  acquisition.method = Trust:

  definition basis equals
    transferor.basis + transferor.gain_or_loss of acquisition.moment
*/

@(c) Gift or transfer in trust before January 1, 1921@
If the property was acquired by gift or transfer in trust on or before December 
31, 1920, the basis shall be the fair market value of such property at the time 
of such acquisition.
/*
scope BasisOfGift under condition acquisition.moment <= |01/31/1920|:
  definition basis equals
    value.fair_market of acquisition.moment
*/

@@(d) Increased basis for gift tax paid@@+

@(1) In general@ If—
(A) the property is acquired by gift on or after September 2, 1958, the basis 
shall be the basis determined under subsection (a), increased (but not above 
the fair market value of the property at the time of the gift) by the amount 
of gift tax paid with respect to such gift, or
/*
scope BasisOfGift under condition
  acquisition.moment >= |09/02/1958| and
  acquisition.method = Gift:

  definition basis equals
    basis_subsection_a + (
      if gift_tax_paid >= value.fair_market of acquisition.moment then
        value.fair_market of acquisition.moment
      else
        gift_tax_paid
    )
*/
(B) the property was acquired by gift before September 2, 1958, and has not been 
sold, exchanged, or otherwise disposed of before such date, the basis of the 
property shall be increased on such date by the amount of gift tax paid with 
respect to such gift, but such increase shall not exceed an amount equal to the 
amount by which the fair market value of the property at the time of the gift 
exceeded the basis of the property in the hands of the donor at the time of the 
gift.
/*
scope BasisOfGift under condition
  acquisition.moment < |09/02/1958| and
  acquisition.method = Gift and
  acquisition.no_sale_or_exchange_before:

  definition basis equals
    basis_subsection_a + (
      if
        gift_tax_paid >=
        value.fair_market of acquisition.moment - transferor.basis
      then
        value.fair_market of acquisition.moment - transferor.basis
      else
        gift_tax_paid
    )
*/

@(2) Amount of tax paid with respect to gift@
For purposes of paragraph (1), the amount of gift tax paid with respect to any 
gift is an amount which bears the same ratio to the amount of gift tax paid under 
chapter 12 with respect to all gifts made by the donor for the calendar year 
(or preceding calendar period) in which such gift is made as the amount of such 
gift bears to the taxable gifts (as defined in section 2503(a) but computed 
without the deduction allowed by section 2521) made by the donor during such 
calendar year or period. For purposes of the preceding sentence, the amount of 
any gift shall be the amount included with respect to such gift in determining 
(for the purposes of section 2503(a)) the total amount of gifts made during the 
calendar year or period, reduced by the amount of any deduction allowed with 
respect to such gift under section 2522 (relating to charitable deduction) or 
under section 2523 (relating to marital deduction).
/*
# We don't formalize the amount of gift tax since it would require 
#formalizing other sections of the code
*/


@(3) Gifts treated as made one-half by each spouse@
For purposes of paragraph (1), where the donor and his spouse elected, under 
section 2513 to have the gift considered as made one-half by each, the amount 
of gift tax paid with respect to such gift under chapter 12 shall be the sum 
of the amounts of tax paid with respect to each half of such gift (computed in 
the manner provided in paragraph (2)).
/*
# Same here
*/

@(4) Treatment as adjustment to basis@
For purposes of section 1016(b), an increase in basis under paragraph (1) shall 
be treated as an adjustment under section 1016(a).
/*
# Same here
*/

@(5) Application to gifts before 1955@
With respect to any property acquired by gift before 1955, references in this 
subsection to any provision of this title shall be deemed to refer to the 
corresponding provision of the Internal Revenue Code of 1939 or prior revenue 
laws which was effective for the year in which such gift was made.
/*
# Same here
*/

@@(6) Special rule for gifts made after December 31, 1976@@++

@(A) In general@
In the case of any gift made after December 31, 1976, the increase in basis 
provided by this subsection with respect to any gift for the gift tax paid under 
chapter 12 shall be an amount (not in excess of the amount of tax so paid) which
bears the same ratio to the amount of tax so paid as—

(i) the net appreciation in value of the gift, bears to

(ii) the amount of the gift.
/*
scope BasisOfGift under condition
  acquisition.moment > |09/02/1976| and
  acquisition.method = Gift:

  definition basis_bonus_after_1976 equals
    gift_tax_paid * (
      value.net_appreciation /
      value.last_acquisition # is it really? "amount" is imprecise
    )
*/

@(B) Net appreciation@
For purposes of paragraph (1), the net appreciation in value of any gift is the 
amount by which the fair market value of the gift exceeds the donor’s adjusted 
basis immediately before the gift.
/*
scope BasisOfGift:
  definition value.net_appreciation equals
    value.fair_market_value of acquisition.moment - transferor.basis
*/

@(e) Gifts between spouses@
In the case of any property acquired by gift in a transfer described in section 
1041(a), the basis of such property in the hands of the transferee shall be 
determined under section 1041(b)(2) and not this section.
/*
# Same here
*/
