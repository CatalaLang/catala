# 83. Property transferred in connection with performance of services

```catala-metadata
declaration scope Transferred_property_83:
  input fair_market_value_unrestricted content money
  input fair_market_value_when_transfered content money
  input amount_paid content money
  input elects_to_include_in_year_of_transfer content boolean
    # has to be explicitely selected within 30 days of transfer
  input substantial_risk_of_forfeiture content boolean
  input transferable content boolean
  input section_421_applies content boolean
  input transfer_401a_or_404a2 content boolean
  input option_without_ascertainable_fair_market_value content boolean
  input exercise_of_option_with_readily_ascertainable_fair_market_value_at_grant content boolean
  input group_term_life_insurance_79 content boolean

  internal is_restricted content boolean
  output amount_to_include_in_gross_income content TransferredReturn

declaration enumeration TransferredReturn:
  -- DoesNotApply
  -- NotThisYear
  -- IncludeInGrossIncome content money
```

## (a) General rule
If, in connection with the performance of services, property is transferred to
any person other than the person for whom such services are performed, the
excess of—

- (1) the fair market value of such property (determined without regard to any
  restriction other than a restriction which by its terms will never lapse) at
  the first time the rights of the person having the beneficial interest in such
  property are transferable or are not subject to a substantial risk of
  forfeiture, whichever occurs earlier, over

- (2) the amount (if any) paid for such property,

shall be included in the gross income of the person who performed such services
in the first taxable year in which the rights of the person having the
beneficial interest in such property are transferable or are not subject to a
substantial risk of forfeiture, whichever is applicable. The preceding sentence
shall not apply if such person sells or otherwise disposes of such property in
an arm’s length transaction before his rights in such property become
transferable or not subject to a substantial risk of forfeiture.

```catala
scope Transferred_property_83:
  label section_83a
  exception section_83_default
  definition amount_to_include_in_gross_income
    under condition (not is_restricted)
    consequence equals
# Note: should not apply if the property was already sold
      if fair_market_value_unrestricted >$ amount_paid
      then IncludeInGrossIncome content (fair_market_value_unrestricted -$ amount_paid)
      else IncludeInGrossIncome content $0
```

## (b) Election to include in gross income in year of transfer
### (1) In general
Any person who performs services in connection with which property is
transferred to any person may elect to include in his gross income for the
taxable year in which such property is transferred, the excess of—
- (A) the fair market value of such property at the time of transfer (determined
  without regard to any restriction other than a restriction which by its terms
  will never lapse), over
- (B) the amount (if any) paid for such property.

If such election is made, subsection (a) shall not apply with respect to the
transfer of such property, and if such property is subsequently forfeited, no
deduction shall be allowed in respect of such forfeiture.

```catala
scope Transferred_property_83:
  label section_83b
  exception section_83a
  definition amount_to_include_in_gross_income
    under condition elects_to_include_in_year_of_transfer
    consequence equals
      if fair_market_value_when_transfered >$ amount_paid
      then IncludeInGrossIncome content (fair_market_value_when_transfered -$ amount_paid)
      else IncludeInGrossIncome content $0

  label section_83_default
  definition amount_to_include_in_gross_income equals NotThisYear
  # Default case

# "if such property is subsequently forfeited, ..." applies to the future and
# not to the current computation, so we don't encode it here
```

### (2) Election
An election under paragraph (1) with respect to any transfer of property shall
be made in such manner as the Secretary prescribes and shall be made not later
than 30 days after the date of such transfer. Such election may not be revoked
except with the consent of the Secretary.

```catala
# This section describes how the election should be made, it doesn't directly
# concern the computation
```

## (c) Special rules
For purposes of this section—
### (1) Substantial risk of forfeiture
The rights of a person in property are subject to a substantial risk of
forfeiture if such person’s rights to full enjoyment of such property are
conditioned upon the future performance of substantial services by any
individual.
### (2) Transferability of property
The rights of a person in property are transferable only if the rights in such
property of any transferee are not subject to a substantial risk of forfeiture.
### (3) Sales which may give rise to suit under section 16(b) of the Securities Exchange Act of 1934
So long as the sale of property at a profit could subject a person to suit under
section 16(b) of the Securities Exchange Act of 1934, such person’s rights in
such property are—
- (A) subject to a substantial risk of forfeiture, and
- (B) not transferable.
### (4) For purposes of determining an individual’s basis in property transferred in connection with the performance of services, rules similar to the rules of section 72(w) shall apply.

```catala
scope Transferred_property_83:
  definition is_restricted equals substantial_risk_of_forfeiture or not transferable
```

## (e) Applicability of section
This section shall not apply to—
- (1) a transaction to which section 421 applies,
- (2) a transfer to or from a trust described in section 401(a) or a transfer
  under an annuity plan which meets the requirements of section 404(a)(2),
- (3) the transfer of an option without a readily ascertainable fair market value,
- (4) the transfer of property pursuant to the exercise of an option with a
  readily ascertainable fair market value at the date of grant, or
- (5) group-term life insurance to which section 79 applies.

```catala
scope Transferred_property_83:
  exception section_83b
  definition amount_to_include_in_gross_income
    under condition
      section_421_applies or
      transfer_401a_or_404a2 or
      option_without_ascertainable_fair_market_value or
      exercise_of_option_with_readily_ascertainable_fair_market_value_at_grant or
      group_term_life_insurance_79
    consequence equals
      DoesNotApply
```

## (h) Deduction by employer
In the case of a transfer of property to which this section applies or a
cancellation of a restriction described in subsection (d), there shall be
allowed as a deduction under section 162, to the person for whom were performed
the services in connection with which such property was transferred, an amount
equal to the amount included under subsection (a), (b), or (d)(2) in the gross
income of the person who performed such services. Such deduction shall be
allowed for the taxable year of such person in which or with which ends the
taxable year in which such amount is included in the gross income of the person
who performed such services.

```catala
declaration scope Deduction_by_employer:
  output deduction_under_section_162 content money


```
