# 虚构的个税规则

## 第一条

个人所得税征收额度是个人年收入的固定百分比。

```catala-metadata
声明 结构 个人：
  数据 年收入 内容 货币
  数据 子女数目 内容 整数
  
声明 枚举 免税额度：
  -- 无免税额度
  -- 子女福利免税额度 内容 整数
  
声明 语境 个税计算：
  输入 某甲 内容 个人
  内部 固定百分比 内容 小数
  输出 个税 内容 货币 
```

```catala
语境 个税计算：
  定义 个税 等于
    某甲.年收入 *￥ 固定百分比
# *￥ 表示有货币乘数
```
## 第二条

第一条提到的固定百分比等于百分之二十。

```catala
# 补充定义
语境 个税计算：
   定义 固定百分比 等于 20%
# 数字在程序里统一用阿拉伯数字表示   
```
## 第三条

对于有两个或以上子女的个人，第一条所指的固定百分比为百分之十五。

```catala
语境 个税计算：
  定义 固定百分比 在条件下
    某甲.子女数目 >= 2
  结果 等于 15%
# 注意数据获取运算符为英文句号(.)  
```

### 第三条补充

对第三条提及的子女的限制条件。 

```catala
声明 语境 子女限制条件：
   输入 年龄 内容 整数
   输出 适用第三条 条件
   
语境 子女限制条件：
  规定 适用第三条 在条件下
     年龄 < 18
  结果 满足
```

## 第四条

两级超额累进税费等于落在每一级内的金额乘以该级税率后之和。

```catala-metadata
声明 结构 两级超额累进:
  数据 分界线 内容 货币
  数据 税率1 内容 小数
  数据 税率2 内容 小数
  
声明 语境 两级超额累进计算:
  输入 税率表 内容 两级超额累进
  输出 税费函数 内容 货币 取决于 货币
# 提供税率表，生成相应函数
```

```catala
语境 两级超额累进计算:
  定义 税费函数 用于 应纳税所得额 等于
    # “应纳税所得额” 是税费函数的形式参数，也可以用别的名字
    如果 应纳税所得额 <=￥ 税率表.分界线 那么
      应纳税所得额 *￥ 税率表.税率1
    否则 (
      税率表.分界线 *￥ 税率表.税率1 +￥ 
      (应纳税所得额 -￥ 税率表.分界线) *￥ 税率表.税率2
    )     
```

## 第五条

两级超额累进税率分界线为￥100,000，基本税率20%，超额税率40%。

```catala
声明 语境 分级个税计算:
  调用 语境 两级超额累进计算
  # "调用" 不是关键词，只是一个子语境的名字
  输入 某甲 内容 个人
  输出 税费 内容 货币
  
语境 分级个税计算:
  定义 调用.税率表 等于    
    两级超额累进 {
    -- 分界线 : ￥100,000
    -- 税率1 : 20%
    -- 税率2 : 40% 
    } 
  定义 税费 等于 
    调用.税费函数 用于 某甲.年收入
```

## 第六条

年收入 ￥10000 以下免税。 

```catala
语境 分级个税计算:
 定义 税费 在条件下
   某甲.年收入 <=￥ ￥10,000
 结果 等于 ￥0
```
### 代码更正

```catala
声明 语境 更正分级个税计算:
  调用 语境 两级超额累进计算
  输入 某甲 内容 个人
  输出 税费函数 内容 货币 取决于 货币
  上下文 输出 税费 内容 货币

语境 更正分级个税计算:
  定义 调用.税率表 等于    
    两级超额累进 {
    -- 分界线 : ￥100,000
    -- 税率1 : 20%
    -- 税率2 : 40% 
    } 
  定义 税费函数 用于 应纳税所得额 等于
    调用.税费函数 用于 应纳税所得额
  标记 第五条
  定义 税费 等于 
    调用.税费函数 用于 某甲.年收入
  例外 第五条
  定义 税费 在条件下
    某甲.年收入 <=￥ ￥10,000
  结果 等于 ￥0
```
### 第六条补充

有7个或以上子女的个人豁免第一条所指税费。

```catala
语境 更正分级个税计算:
  # one more exception
  例外 第五条
  定义 税费 在条件下 
    某甲.子女数目 >= 7
  结果 等于 ￥0
```

### 测试代码

```catala  
声明 语境 测试2:
  调用 语境 更正分级个税计算
  输出 税费 内容 货币
  
语境 测试2:
  定义 调用.某甲 等于
  个人 {
  -- 年收入 : ￥4,000
  -- 子女数目 : 6
  }
  定义 税费 等于 调用.税费
  声称 税费 = ￥0
  # 省略人民币符号会导致内部类型错误 
```

## 第七条 

对个人违法行为的罚款金额以个税金额为参考。为此目的，年收入￥10,000以及下的个人，
个税金额应视为￥500。

```catala
声明 语境 罚款参考金额:
  调用 语境 更正分级个税计算
  输入 某甲 内容 个人
  输出 参考金额 内容 货币
  
语境 罚款参考金额:  
  定义 调用.某甲 等于 某甲
  定义 参考金额 等于 调用.税费
  定义 调用.税费 在条件下
    某甲.年收入 <=￥ ￥10,000
  结果 等于 ￥500
  # 因为小语境的输出类型为“上下文”，所以大语境
  # 可以直接制定小语境的输出，从而绕过小语境的
  # 内部计算逻辑
```

### 测试代码

```catala
声明 语境 测试3:
  调用 语境 罚款参考金额
  输出 参考金额 内容 货币
  
语境 测试3:
  定义 调用.某甲 等于
  个人 {
  -- 年收入 : ￥100,000
  -- 子女数目 : 5
  } 
  定义 参考金额 等于 调用.参考金额
  声称 参考金额 = ￥20,000 
  # 对于收入在￥10,000或以下的个人，其子女数目与
  # 罚款参考金额无关  
```

