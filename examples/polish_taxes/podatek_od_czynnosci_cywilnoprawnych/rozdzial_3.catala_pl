### Rozdział 3

#### Art. 7.

1. Stawki podatku wynoszą:

1) od umowy sprzedaży:

a) nieruchomości, rzeczy ruchomych, prawa użytkowania wieczystego, własnościowego spółdzielczego prawa do lokalu mieszkalnego, spółdzielczego prawa do lokalu użytkowego oraz wynikających z przepisów prawa spółdzielczego: prawa do domu jednorodzinnego oraz prawa do lokalu w małym domu mieszkalnym – 2%,

```catala
deklaracja zakres UmowaSprzedaży:
  kontekst kwota typu pieniądze
  kontekst inne_prawa_majątkowe warunek
  kontekst stawka_podatku typu dziesiętny
  wyjście podatek typu pieniądze

zakres UmowaSprzedaży:
  definicja podatek wynosi
    kwota * stawka_podatku

zakres UmowaSprzedaży:
  etykieta a7_u1_p1_ppa
  definicja stawka_podatku wynosi 2%
```


b) innych praw majątkowych – 1%;
```catala
zakres UmowaSprzedaży:
  wyjątek a7_u1_p1_ppa
  definicja stawka_podatku pod warunkiem
    inne_prawa_majątkowe
  konsekwencja wynosi 1%
```

2) od umów zamiany, dożywocia, o dział spadku, o zniesienie współwłasności oraz darowizny:

a) przy przeniesieniu własności nieruchomości, rzeczy ruchomych, prawa użytkowania wieczystego, własnościowego spółdzielczego prawa do lokalu
mieszkalnego, spółdzielczego prawa do lokalu użytkowego oraz wynikających z przepisów prawa spółdzielczego: prawa do domu jednorodzinnego oraz prawa do lokalu w małym domu mieszkalnym – 2%,

```catala
deklaracja zakres UmowaZamianyLubDarowizny:
  kontekst kwota typu pieniądze
  kontekst inne_prawa_majątkowe warunek
  kontekst stawka_podatku typu dziesiętny
  wyjście podatek typu pieniądze

zakres UmowaZamianyLubDarowizny:
  definicja podatek wynosi
    kwota * stawka_podatku

zakres UmowaZamianyLubDarowizny:
  etykieta a7_u1_p2_ppa
  definicja stawka_podatku wynosi 2%
```

b) przy przeniesieniu własności innych praw majątkowych – 1%;

```catala
zakres UmowaZamianyLubDarowizny:
  wyjątek a7_u1_p2_ppa
  definicja stawka_podatku pod warunkiem
    inne_prawa_majątkowe
  konsekwencja wynosi 1%
```

3) od umowy ustanowienia odpłatnego użytkowania, w tym nieprawidłowego, oraz odpłatnej służebności – 1%, z zastrzeżeniem ust. 5;

```catala
deklaracja zakres OdpłatneUżytkowanie:
  kontekst kwota typu pieniądze
  kontekst stawka_podatku typu dziesiętny
  wyjście podatek typu pieniądze

zakres OdpłatneUżytkowanie:
  definicja podatek wynosi
    kwota * stawka_podatku

zakres OdpłatneUżytkowanie:
  definicja stawka_podatku wynosi 1%
```

4) od umowy pożyczki oraz depozytu nieprawidłowego – 0,5%, z zastrzeżeniem ust. 5;

```catala
deklaracja zakres PożyczkaLubDepozytNieprawidłowy:
  kontekst kwota typu pieniądze
  kontekst stawka_podatku typu dziesiętny
  wyjście podatek typu pieniądze

zakres PożyczkaLubDepozytNieprawidłowy:
  definicja podatek wynosi
    kwota * stawka_podatku # TODO: extract somehow?
                            # this exists in every declaration

zakres PożyczkaLubDepozytNieprawidłowy:
  definicja stawka_podatku wynosi 0.5%
```

7) od ustanowienia hipoteki:

a) na zabezpieczenie wierzytelności istniejących – od kwoty zabezpieczonej wierzytelności – 0,1%,

```catala
deklaracja  zakres Hipoteka:
  kontekst kwota typu pieniądze
  kontekst wysokość_nieustalona warunek
  kontekst stawka_podatku typu dziesiętny
  kontekst podatek typu pieniądze

zakres Hipoteka:
  etykieta a7_u1_p7_ppa
  definicja podatek wynosi
    kwota * stawka_podatku

zakres Hipoteka:
  definicja stawka_podatku wynosi 0.1%
```

b) na zabezpieczenie wierzytelności o wysokości nieustalonej – 19 zł;

```catala
zakres Hipoteka:
  wyjątek a7_u1_p7_ppa
  definicja podatek pod warunkiem
    wysokość_nieustalona
  konsekwencja wynosi 19 PLN
```

9) od umowy spółki – 0,5%.

```catala
deklaracja zakres UmowaSpółki:
  kontekst kwota typu pieniądze
  kontekst stawka_podatku typu dziesiętny
  wyjście podatek typu pieniądze

zakres UmowaSpółki:
  definicja podatek wynosi
    kwota * stawka_podatku

zakres UmowaSpółki:
  definicja stawka_podatku wynosi 0.5%
```
