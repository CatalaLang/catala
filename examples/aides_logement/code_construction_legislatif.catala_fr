
## Partie législative

### Livre VIII : Aides personnelles au logement

#### Titre II : Dispositions communes aux aides personnelles au logement

##### Chapitre Ier : Principes généraux

###### Section 1 : Définition

####### Article L821-1 | LEGIARTI000038814980

Les aides personnelles au logement ainsi que les primes accordées
aux bénéficiaires de ces aides afin qu'ils déménagent pour s'assurer
des conditions de logement plus adaptées sont régies par le présent livre.

Les aides personnelles au logement comprennent :

1° L'aide personnalisée au logement ;

2° Les allocations de logement :

a) L'allocation de logement familiale ;

b) L'allocation de logement sociale.

```catala-metadata
déclaration énumération TypeAidesPersonnelleLogement:
  -- AidePersonnaliséeLogement
  -- AllocationLogementFamiliale
  -- AllocationLogementSociale
```
###### Section 2 : Règles de non-cumul

####### Sous-section 1 : Aides personnelles au logement

######## Article L821-2 | LEGIARTI000038814974

Les aides personnelles au logement sont accordées au titre de
la résidence principale.

```catala
champ d'application ÉligibilitéAidesPersonnelleLogement:
  règle condition_logement_résidence_principale sous condition
    ménage.logement.résidence_principale
  conséquence rempli
```

####### Sous-section 2 : Primes de déménagement

######## Article L821-4 | LEGIARTI000038814968

Les primes de déménagement régies par le présent livre ne peuvent se cumuler
avec d'autres primes, allocations ou indemnités, quelle qu'en soit l'origine
ou le fondement juridique, également destinées à couvrir des frais de
déménagement.

Toutefois, lorsque le montant ainsi versé est inférieur à celui de la prime
de déménagement à laquelle la personne ou le ménage aurait droit en vertu des
dispositions de l'article L. 823-8 , la différence est versée par l'organisme
payeur.

```catala
# Nous décidons de ne pas coder les règles de non-cumul pour la prime de
# déménagement.
```

##### Chapitre II : Conditions générales d'attribution

```catala
# Ce chapitre définit diverses conditions d'éligibilité concernant le logement.
# La structure de ces conditions comporte des exceptions négatives à une
# condition positive. Il n'y a pas vraiment de bonne manière de modéliser
# cela en Catala pour le moment à part le code ci-dessous qui récapitule
# la méta-structure logique des articles de loi de ce chapitre.
champ d'application ÉligibilitéAidesPersonnelleLogement:

  règle éligibilité_logement sous condition
    # La condition logement est en fait l'amalgame de plusieurs sous-conditions
    # portant toutes sur le logement définies au long des diverses parties
    # du code de la construction et de l'habitation. Attention, ne pas oublier
    # de modifier cette liste en cas de rajout d'une sous-condition.
    condition_logement_résidence_principale et # L821-2
    condition_logement_mode_occupation et
    condition_logement_location_tiers et
    condition_ouverture_l822_10_peuplement_logement
  conséquence rempli

  exception règle éligibilité_logement sous condition
    condition_non_ouverture_l822_8 ou
    condition_non_ouverture_l822_9_decence_logement
  conséquence non rempli
```

###### Article L822-1 | LEGIARTI000038814948

Les dispositions du présent livre relatives au bénéficiaire, à
la résidence principale ou à la prise en compte des ressources
applicables au conjoint, sont applicables, dans les mêmes conditions,
au partenaire lié par un pacte civil de solidarité ou au concubin.

```catala-metadata
déclaration énumération SituationFamiliale:
  -- Célibataire
  -- Mariés contenu date # Date du mariage, utilisée par L841-1
  -- Pacsés
  -- Concubins
  -- CélibataireSéparéDeFait # Voir R821-3
  -- ConcubinageDontSéparéDeFait # Voir R821-3
```

###### Article L822-2 | LEGIARTI000038814944

I.-Peuvent bénéficier d'une aide personnelle au logement :

1° Les personnes de nationalité française ;

2° Les personnes de nationalité étrangère remplissant les conditions
prévues par les deux premiers alinéas de l' article L. 512-2 du
code de la sécurité sociale .

```catala
champ d'application ÉligibilitéAidesPersonnelleLogement:
  définition condition_nationalité égal à
    selon demandeur.nationalité sous forme
    -- Française: vrai
    -- Étrangère de conditions:
      conditions.satisfait_conditions_l512_2_code_sécurité_sociale
```

II.-Parmi les personnes mentionnées au I, peuvent bénéficier d'une aide
personnelle au logement celles remplissant les conditions prévues par le
présent livre pour son attribution qui sont locataires, résidents en
logement-foyer ou qui accèdent à la propriété d'un local à usage exclusif
d'habitation et constituant leur résidence principale.

Les sous-locataires, sous les mêmes conditions, peuvent également en bénéficier.

```catala
champ d'application ÉligibilitéAidesPersonnelleLogement:
  étiquette l822_2 règle condition_logement_mode_occupation sous condition
    selon ménage.logement.mode_occupation sous forme
      -- Locataire: vrai
      -- RésidentLogementFoyer: vrai
      -- AccessionPropriétéLocalUsageExclusifHabitation:
        ménage.logement.résidence_principale
      -- SousLocataire: vrai
      -- LocationAccession: vrai # Justifié par L831-2, avec une
                                 # exception à venir
  conséquence rempli
```

###### Article L822-3 | LEGIARTI000038814942

Les aides personnelles au logement ne sont pas dues aux personnes locataires
d'un logement dont elles-mêmes, leurs conjoints ou l'un de leurs ascendants
ou descendants, jouissent d'une part de la propriété ou de l'usufruit,
personnellement ou par l'intermédiaire de parts sociales de sociétés,
quels que soient leurs formes et leurs objets.

```catala
champ d'application ÉligibilitéAidesPersonnelleLogement:
  définition usufruit_ou_propriété_famille égal à
    (selon ménage.logement.propriétaire sous forme
      -- DemandeurOuConjointOuParentOuViaPartsSociétés de parts: vrai
      -- ParentOuAutre.Autre: faux) ou
    (selon ménage.logement.usufruit sous forme
      -- DemandeurOuConjointOuParentOuViaPartsSociétés de parts: vrai
      -- ParentOuAutre.Autre: faux)

  étiquette l822_3_1 exception l822_2
  règle condition_logement_mode_occupation sous condition
    selon ménage.logement.mode_occupation sous forme
      -- Locataire: usufruit_ou_propriété_famille
      -- n'importe quel: faux
  conséquence non rempli
```

Par dérogation à la règle posée au premier alinéa, ces aides peuvent être
versées si l'ensemble des parts de propriété et d'usufruit du logement
ainsi détenues est inférieur à des seuils fixés par voie réglementaire.
Ces seuils ne peuvent excéder 20 % de la propriété ou de l'usufruit du logement.

```catala
champ d'application ÉligibilitéAidesPersonnelleLogement:
  étiquette l822_3_2 exception l822_3_1
  règle condition_logement_mode_occupation sous condition
    usufruit_ou_propriété_famille et
    (selon ménage.logement.propriétaire sous forme
      -- DemandeurOuConjointOuParentOuViaPartsSociétés de parts: parts
      -- ParentOuAutre.Autre: 0,0) < seuil_l822_3_parts_propriété et
    (selon ménage.logement.usufruit sous forme
      -- DemandeurOuConjointOuParentOuViaPartsSociétés de parts: parts
      -- ParentOuAutre.Autre: 0,0) < seuil_l822_3_parts_usufruit
  conséquence rempli
  # Les < sont stricts par mention de R822-1

  assertion seuil_l822_3_parts_propriété < 20 %
  assertion seuil_l822_3_parts_usufruit < 20 %
```

###### Article L822-4 | LEGIARTI000038814940

Les aides personnelles au logement ne sont pas dues si le local est loué ou
sous-loué en partie à des tiers, sauf s'il s'agit d'une personne âgée ou
handicapée adulte qui a passé un contrat conforme aux dispositions de
l' article L. 442-1 du code de l'action sociale et des familles ou d'une
personne de moins de trente ans.

```catala
champ d'application ÉligibilitéAidesPersonnelleLogement:
  étiquette cas_base_l822_4 règle condition_logement_location_tiers
  sous condition
    ménage.logement.loué_ou_sous_loué_à_des_tiers sous forme
      LouéOuSousLouéÀDesTiers.Non
  conséquence non rempli

exception cas_base_l822_4 règle condition_logement_location_tiers
  sous condition
    selon ménage.logement.loué_ou_sous_loué_à_des_tiers sous forme
    -- LouéOuSousLouéÀDesTiers.Non: vrai
    -- LouéOuSousLouéÀDesTiers.Oui de personne:
      (résultat de VérificationÂgeSupérieurÀ avec
        { -- date_naissance: personne.date_naissance_personne_sous_location
          -- date_courante: date_courante
          -- années: 30 an }).est_supérieur
        ou
      personne.conforme_article_l442_1
  conséquence rempli
```

Pour l'attribution d'une aide personnelle au logement, ces personnes sont
assimilées à des locataires, au titre de la partie du logement qu'elles
occupent.

```catala
# Pas d'ajout nécessaire
```

Toutefois, les conditions fixées à l'article L. 822-3 du présent code
s'appliquent également au locataire, au sous-locataire et au propriétaire.

```catala
# Pas d'ajout nécessaire
```

Pour l'attribution d'une aide personnelle au logement, en cas d'intermédiation
locative et en cas d'application des dispositions de l'article L. 442-8-1 du
présent code prévoyant une sous-location totale du logement, le sous-locataire
est assimilé au locataire.



###### Article L822-5 | LEGIARTI000038814936

Les aides personnelles au logement ne sont dues qu'aux personnes payant un minimum
de loyer, compte tenu de leurs ressources et de la valeur en capital de leur
patrimoine, lorsque cette valeur est supérieure à un montant fixé par voie
réglementaire.

```catala
# Cet article est une déclaration d'intention qui donne des propriétée sur
# la formule de calcul. Dans le futur il serait interessant de vérifier que
# la formule est bien de cette forme là.
champ d'application ÉligibilitéAidesPersonnelleLogement:
  définition patrimoine_total_demandeur égal à
    demandeur.patrimoine.produisant_revenu_période_r822_3_3_r822_4 +
    demandeur.patrimoine.ne_produisant_pas_revenu_période_r822_3_3_r822_4

  étiquette l822_5_1 définition patrimoine_pris_en_compte égal à
    si patrimoine_total_demandeur > seuil_l822_5_patrimoine alors
      patrimoine_total_demandeur
    sinon
      0€
```

Par dérogation à la règle énoncée au premier alinéa, lorsque le demandeur d'une
aide personnelle au logement ou son conjoint est bénéficiaire de l'allocation
aux adultes handicapés prévue à l'article L. 821-1 du code de la sécurité sociale
ou de l'allocation d'éducation de l'enfant handicapé prévue à l'article L. 541-1
du même code , la valeur en capital du patrimoine appréciée pour l'ensemble du ménage
n'est pas prise en compte dans le calcul de l'aide personnelle au logement.

```catala
champ d'application ÉligibilitéAidesPersonnelleLogement:
  exception r822_22_3 définition patrimoine_pris_en_compte
  sous condition existe prestation parmi ménage.prestations_reçues tel que
    (prestation = AllocationSoutienEnfantHandicapé ou
     prestation = AllocationAdulteHandicapé)
  conséquence égal à 0€
```

La même dérogation s'applique au demandeur d'une aide personnelle au logement
résidant dans un établissement d'hébergement pour personnes âgées dépendantes
ou dans une résidence autonomie mentionnés à l'article L. 313-12 du code de
l'action sociale et des familles .

```catala
champ d'application ÉligibilitéAidesPersonnelleLogement:
  exception r822_22_3 définition patrimoine_pris_en_compte
  sous condition ménage.logement.est_ehpad_ou_maison_autonomie_l313_12_asf
  conséquence égal à 0€
```


###### Article L822-6 | LEGIARTI000038814934


La détermination ainsi que les conditions de prise en compte des ressources et de
la valeur du patrimoine sont définies par voie réglementaire.


Les conditions de prise en compte des ressources, notamment les périodes de référence
retenues, peuvent varier en fonction de la nature des ressources.

```catala
# Cet article est une déclaration d'intention qui donne des propriétés sur
# la formule de calcul. Dans le futur il serait interessant de vérifier que
# la formule est bien de cette forme là.
```

###### Article L822-7 | LEGIARTI000038814932

La prise en compte des ressources peut relever de dispositions spécifiques,
lorsque le demandeur est âgé de moins de vingt-cinq ans et qu'il dispose d'un
contrat de travail autre qu'un contrat à durée indéterminée.

```catala
# Information donnée par le bureau DGALN/DHUP/FE4 le 25/05/2022:
# "Historiquement, cet article avait été créé pour déroger (pour les moins
# de 25 ans) au dispositif « d’évaluation forfaitaire » appliqué,
# avant réforme,
# pour les ménages présentant un revenu moindre en N-2 mais ayant une activité
# professionnelle lors de la demande ou du recalcul du droit (anciens articles
# R. 822-18 à 20 du CCH abrogés en 2020). Cet article n’a de fait plus lieu
# d’être (il n’a plus d’application concrète, l’évaluation forfaitaire ayant
# été supprimée) mais n’a pas pu être abrogé en même temps, l’abrogation ayant
# eu lieu par un vecteur réglementaire."

# Il n'y a donc rien à formaliser ici en Catala.
```


###### Article L822-8 | LEGIARTI000038814930

Les personnes rattachées au foyer fiscal de leurs parents, lorsque ces derniers
sont assujettis à l'impôt sur la fortune immobilière en application de l' article
964 du code général des impôts , ne peuvent bénéficier d'aucune aide personnelle au
logement. Cette condition est appréciée pour chacun des membres du ménage.


```catala
# La condition, si elle est vérifiée par un membre du ménage
# enlève-t-elle le droit aux APL pour l'ensemble des membres du ménage ?
# => Réponse de DGALN/DHUP/FE4 le 25/05/2022: OUI.

# L'interpretation est donc qu'aucun membre du ménage ne doit être
# rattaché à des parents payant l'ifi.
champ d'application ÉligibilitéAidesPersonnelleLogement:
  règle condition_non_ouverture_l822_8
  sous condition ménage.condition_rattaché_foyer_fiscal_parent_ifi
  conséquence rempli
```

###### Article L822-9 | LEGIARTI000038814926

Pour ouvrir droit à une aide personnelle au logement, le logement doit répondre à
des exigences de décence définies en application des deux premiers alinéas de
l' article 6 de la loi n° 89-462 du 6 juillet 1989 tendant à améliorer les rapports
locatifs et portant modification de la loi n° 86-1290 du 23 décembre 1986.

```catala
champ d'application ÉligibilitéAidesPersonnelleLogement:
  règle condition_non_ouverture_l822_9_decence_logement
  sous condition non ménage.logement.logement_decent_l89_462
  conséquence rempli
```

Lorsque le logement est loué en colocation, formalisée par la conclusion de plusieurs
contrats entre les locataires et le bailleur, il est tenu compte, pour apprécier
s'il répond à ces mêmes exigences, de l'ensemble des éléments, équipements et
pièces dont dispose chaque colocataire.

```catala
# Ne nécessite pas de code particulier
```

Les modalités d'application du présent article sont fixées par voie réglementaire.

###### Article L822-10 | LEGIARTI000038814924

L'attribution d'une aide personnelle au logement est subordonnée au
respect de conditions de peuplement des logements.

```catala
# Cet article fait référence à la condition sur la surface minimale du logement
# de R822-25
champ d'application ÉligibilitéAidesPersonnelleLogement:
  règle condition_ouverture_l822_10_peuplement_logement
  sous condition condition_logement_surface
  conséquence rempli
```

Les modalités d'application du présent article sont fixées par voie réglementaire

##### Chapitre III : Modalités de liquidation et de versement

###### Article L823-1 | LEGIARTI000038814920

Le montant des aides personnelles au logement est calculé en fonction d'un barème
défini par voie réglementaire.

Ce barème est établi en prenant en considération :

1° La situation de famille du demandeur et le nombre de personnes à charge
vivant habituellement au foyer ;

2° Ses ressources et la valeur en capital de son patrimoine et, s'il y a lieu,
de son conjoint et des personnes vivant habituellement à son foyer, telles que
définies aux articles L. 822-5 à L. 822-8 ;

3° Le montant du loyer payé, pris en compte dans la limite d'un plafond, ainsi
que les dépenses accessoires retenues forfaitairement ;

4° La qualité du demandeur : locataire, colocataire ou sous-locataire d'un
logement meublé ou non, accédant à la propriété ou résident en logement-foyer.

Pour l'application du 1°, les enfants à charge doivent respecter les conditions
prévues à l'article L. 512-2 du code de la sécurité sociale .

```catala
# Ici nous sommes dans une situation délicate car pour les enfants à charge
# les règles de prise en compte sont énoncées dans R823-4, sauf que le dernier
# alinéa nous rajoutent une condition qui n'est pas dans R823-4.
# Pour prendre en compte cette condition supplémentaire en respectant la
# structure du texte de droit nous choisissons de l'encoder comme une
# exception négative qui vient invalide la règle de R823-4 dans le cas où
# l'enfant étranger ne remplit pas les conditions de l'article
# L512-2 du code de la sécurité sociale.
champ d'application ÉligibilitéAidesPersonnelleLogement:
  # Ici nous définissons cette condition d'applicabilité en apparence
  # toujours vraie mais qui sera peut-être invalidée dans le cas de Mayotte
  # (article L861-5).
  règle septième_alinéa_l823_1_applicable rempli

  exception r823_4_enfants
  règle prise_en_compte_personne_à_charge de personne_à_charge sous condition
    septième_alinéa_l823_1_applicable et
    selon personne_à_charge sous forme
    -- EnfantÀCharge de enfant: (
      selon enfant.nationalité sous forme
      -- Étrangère de conditions:
        non conditions.satisfait_conditions_l512_2_code_sécurité_sociale
      -- n'importe quel: faux
    )
    -- n'importe quel: faux
  conséquence non rempli
```

###### Article L823-2 | LEGIARTI000038814918

Pour effectuer le calcul découlant du 1° de l'article L. 823-1 , l'enfant à
charge est rattaché à la personne qui en assume la charge effective et
permanente.

En cas de résidence alternée de l'enfant au domicile de chacun des parents
telle que prévue à l'article 373-2-9 du code civil , mise en œuvre de manière
effective, les parents désignent le bénéficiaire de l'aide.

Cependant, la charge de l'enfant pour le calcul des aides personnelles au
logement est partagée entre les deux parents allocataires, soit sur demande
conjointe des parents, soit si les parents sont en désaccord sur la désignation
du bénéficiaire, selon des modalités définies par voie réglementaire.

```catala
# Voir énumération "SituationGardeAlternée".
```

###### Article L823-3 | LEGIARTI000038814916

Sont assimilées aux loyers :

1° Les mensualités acquittées au titre des charges de remboursement des prêts
contractés pour l'acquisition du logement ou son amélioration ;

2° La redevance déterminée par la loi n° 84-595 du 12 juillet 1984 définissant
la location-accession à la propriété immobilière ;

3° La rémunération de l'opérateur mentionnée au III de l'article 29-11 de la
loi n° 65-557 du 10 juillet 1965 fixant le statut de la copropriété des
immeubles bâtis ;

4° L'indemnité d'occupation mentionnée à l'article L. 615-9 du présent code ;

5° La redevance mentionnée à l'article L. 615-10 du présent code ;

6° L'indemnité, prévue au 4° de l'article L. 442-1 du code de l'action sociale
et des familles , représentative de la mise à disposition de la ou des pièces
réservées à la personne accueillie.

```catala
# Ce n'est pas très intéressant de faire apparaître dans l'entrée du calcul
# le détail de ce qui est inclus dans le terme "loyer". C'est d'ailleurs ce
# que fait déjà la CAF sur ces outils informatiques. Donc le programme
# Catala fera apparaître une unique case "loyers_l823_3"
```

###### Article L823-4 | LEGIARTI000046194144

Le barème est révisé chaque année au 1er octobre.

Sont indexés sur l'évolution de l'indice de référence des loyers défini à
l' article 17-1 de la loi n° 89-462 du 6 juillet 1989 tendant à améliorer
les rapports locatifs et portant modification de la loi n° 86-1290 du 23
décembre 1986 :

1° Les plafonds de loyers ;

2° Le montant forfaitaire des charges ;

3° Les plafonds des charges de remboursement des contrats de prêts dont
la signature est postérieure à la date de révision du barème ;

4° Les équivalences de loyer et de charges locatives ;

5° Le terme constant de la participation personnelle du ménage.

La date de l'indice de référence des loyers prise en compte pour cette
révision est celle du deuxième trimestre de l'année en cours.

```catala
# Il n'est pas vraiment utile de coder pour notre calcul les règles que
# doivent respecter les décrets qui chaque année mettent à jour les différents
# éléments du barème de calcul. Même s'il pourrait être utile plus tard de
# vérifier que leur évolution est bien conforme à L823-4.
```

###### Article L823-5 | LEGIARTI000038814912

Les modalités d'ouverture et d'extinction des droits sont fixées par voie
réglementaire.

```catala
# Rien à faire ici.
```

###### Article L823-6 | LEGIARTI000038814910

Le bailleur auprès duquel l'aide est versée signale le déménagement de
l'allocataire et la résiliation de son bail.

Si l'allocataire procède à un remboursement anticipé de son prêt, le prêteur
auprès duquel l'aide est versée signale ce remboursement anticipé à
l'organisme payeur.

Les délais dans lesquels ont lieu ces signalements sont définis par voie
réglementaire.

```catala
# Rien à faire ici.
```

###### Article L823-7 | LEGIARTI000038814908

L'aide personnelle au logement peut ne pas être versée lorsque son montant
mensuel est inférieur à un montant, qui peut varier selon la nature de l'aide
et la qualité du demandeur telle qu'elle est définie au 4° de
l'article L. 823-1 .

Ce montant minimal est fixé par voie réglementaire.

```catala
# Voir les états "montant_minimal" dans le calcul des aides finales dans
# le champ d'application CalculAidePersonnaliséeLogement.
```

###### Article L823-8 | LEGIARTI000038814906

Une prime de déménagement est attribuée par les organismes mentionnés à
l'article L. 812-1 aux personnes ou aux ménages qui emménagent dans un
nouveau logement ouvrant droit à une aide personnelle au logement et qui
ont à leur charge un enfant d'un rang déterminé. Le rang de l'enfant et la
période durant laquelle doit avoir lieu le déménagement pour ouvrir droit à
cette prime sont fixés par voie réglementaire.

La prime est due si le droit à l'aide personnelle au logement est ouvert dans
un délai fixé par voie réglementaire à compter de la date d'emménagement, même
lorsqu'en application de l'article L. 823-7 , il n'est pas procédé au versement
de l'aide.

```catala
# Voir le champ d'application ÉligibilitéPrimeDeDéménagement.
champ d'application ÉligibilitéPrimeDeDéménagement:
  règle éligibilité_logement sous condition
    éligibilité_apl.éligibilité et condition_rang_enfant et
    condition_période_déménagement et
    éligibilité_apl.date_courante - date_emménagement
     <= délai_après_emménagement_l823_8_2
  conséquence rempli
```

###### Article L823-9 | LEGIARTI000038814904

Les articles L. 161-1-5 et L. 553-2 du code de la sécurité sociale sont
applicables au recouvrement des montants d'aide personnelle au logement
indûment versés.

```catala
# Hors du champ de ce calcul.
```

#### Titre III : Aide personnalisée au logement

##### Chapitre Ier : Champ d'application

```catala
champ d'application ÉligibilitéAidePersonnaliséeLogement:
  règle éligibilité sous condition
    # La condition est en fait l'amalgame de plusieurs sous-conditions
    # portant toutes sur le logement définies au long des diverses parties
    # du code de la construction et de l'habitation. Attention, ne pas oublier
    # de modifier cette liste en cas de rajout d'une sous-condition.
    condition_logement_bailleur et # L831-1
    condition_logement_prêt et # L831-2
    éligibilité_commune.éligibilité # Pour être éligible à l'aide personnalisée
                                    # au logement, il faut remplir les
                                    # conditions communes.
  conséquence rempli
```

###### Article L831-1 | LEGIARTI000038814882

L'aide personnalisée au logement s'applique aux :

1° Logements occupés par leurs propriétaires, construits, acquis ou améliorés
au moyen d'aides de l'Etat ou de prêts dont les caractéristiques et les
conditions d'octroi sont fixées par voie réglementaire, sous les réserves
énoncées à l'article L. 831-2 ;

```catala
# Les conditions d'octroi pour le prêt sont définities aux articles D331-1
# et suivants du même code. Cela a été confirmé le 25/05/2022 par
# DGALN/DHUP/FE4.

# Ici, le 1° vient affiner le principe général définit à L822-2 selon lequel
# tous les propriétaires ayant un prêt peuvent toucher les APL. En effet, ça
# dépend du prêt (voir articles référés). Donc ici on vient apporter une
# exception à la règle de L822-2 quand les conditions relatives au prêt ne
# sont pas remplies.
champ d'application ÉligibilitéAidePersonnaliséeLogement:
  règle condition_logement_bailleur sous condition
    selon ménage.logement.mode_occupation sous forme
      -- AccessionPropriétéLocalUsageExclusifHabitation de propriété:
        caractéristiques_prêt_l831_1_1 de propriété.prêt
      -- n'importe quel: faux
  conséquence rempli
```

2° Logements à usage locatif appartenant à des organismes d'habitations à
loyer modéré ou gérés par eux ou appartenant aux bailleurs du secteur locatif
définis au quatrième alinéa de l' article 41 ter de la loi n° 86-1290 du 23
décembre 1986 tendant à favoriser l'investissement locatif, l'accession à la
propriété des logements sociaux et le développement de l'offre foncière, à
condition que ces bailleurs s'engagent à respecter les obligations précisées
par des conventions régies par le chapitre III du titre V du livre III , ainsi
que les logements à usage locatif appartenant à d'autres bailleurs, à condition
que ceux-ci s'engagent à respecter les obligations précisées par des conventions
régies par la section 3 du chapitre Ier du titre II du livre III ;

```catala
champ d'application ÉligibilitéAidePersonnaliséeLogement:
  règle condition_logement_bailleur sous condition
    selon ménage.logement.mode_occupation sous forme
      -- Locataire de location:
        (selon location.bailleur sous forme
        -- BailleurSocial de convention:
          convention.
            conventionné_livre_III_titre_V_chap_III
        -- BailleurPrivéAvecConventionnementSocial de convention:
          convention.conventionné_livre_III_titre_II_chap_I_sec_3
        -- n'importe quel: faux)
      -- n'importe quel: faux
  conséquence rempli
```

3° Logements à usage locatif construits, acquis ou améliorés au moyen d'aides de
l'Etat ou de prêts régis par le chapitre III du titre II ou par le titre III du
livre III ; l'octroi de ces aides est subordonné à l'engagement pris par les
bailleurs de respecter les obligations précisées par des conventions régies
par le chapitre III du titre V du livre III ;

```catala
# Cet alinéa apporte une précision sur l'origine de la convention pour le
# bailleur social, mais ne nous intéresse pas pour la formalisation puisque
# tout ce qui nous intéresse c'est de savoir si la convention existe ou pas.
```

4° Logements à usage locatif construits ou améliorés dans des conditions fixées
par voie réglementaire et dont les bailleurs s'engagent à respecter les
obligations précisées par des conventions régies par le chapitre III du titre V
ou par la section 3 du chapitre Ier du titre II du livre III ;

```catala
# Cet alinéa apporte une précision sur l'origine de la convention pour le
# bailleur social, mais ne nous intéresse pas pour la formalisation puisque
# tout ce qui nous intéresse c'est de savoir si la convention existe ou pas.
```

5° Logements-foyers assimilés dans des conditions fixées par voie réglementaire
aux logements mentionnés aux 2° et 3° ci-dessus, dès lors qu'ils font l'objet
des conventions régies par le chapitre III du titre V du livre III ;

```catala
champ d'application ÉligibilitéAidePersonnaliséeLogement:
  étiquette l831_1_alinea_5
  règle condition_logement_bailleur sous condition
    selon ménage.logement.mode_occupation sous forme
      -- RésidentLogementFoyer de location:
        location.conventionné_livre_III_titre_V_chap_III
      -- n'importe quel: faux
  conséquence rempli
```

6° Logements occupés par des titulaires de contrats de location-accession
conclus dans les conditions prévues par la loi n° 84-595 du 12 juillet 1984
définissant la location-accession à la propriété immobilière, lorsque ces
logements ont été construits, améliorés ou acquis et améliorés au moyen d'aides
de l'Etat ou de prêts dont les caractéristiques et les conditions d'octroi sont
fixées par voie réglementaire, sous les réserves énoncées à l ‘ article L. 831-2.

```catala
champ d'application ÉligibilitéAidePersonnaliséeLogement:
  règle condition_logement_bailleur sous condition
    selon ménage.logement.mode_occupation sous forme
      -- LocationAccession de propriété:
        caractéristiques_prêt_l831_1_6 de propriété.prêt
      -- n'importe quel: faux
  conséquence rempli
```

###### Article L831-2 | LEGIARTI000038814880

Les logements qui ont fait l'objet d'un prêt ou d'un contrat de location-accession
mentionné au 1° ou au 6° de l'article L. 831-1 signé après le 31 décembre 2017
n'ouvrent pas droit à l'aide personnalisée au logement.

```catala
champ d'application ÉligibilitéAidePersonnaliséeLogement:
  étiquette l831_2_base règle condition_logement_prêt rempli
  # Cas de base implicite

  étiquette l831_2_1 exception l831_2_base règle condition_logement_prêt
  sous condition
    selon ménage.logement.mode_occupation sous forme
      -- LocationAccession de propriété:
        propriété.prêt.date_signature >= |2017-12-31|
      -- AccessionPropriétéLocalUsageExclusifHabitation de propriété:
        propriété.prêt.date_signature >= |2017-12-31|
      -- n'importe quel: faux
  conséquence non rempli
```

Toutefois, continuent à ouvrir droit à l'aide les logements ayant fait l'objet
des mêmes prêt ou contrat de location-accession signés avant le 1er janvier
2020, dès lors qu'ils répondent à la double condition d'être anciens et
situés dans une commune ne se caractérisant pas par un déséquilibre
important entre l'offre et la demande de logements entraînant des difficultés
d'accès au logement dans le parc résidentiel existant.

```catala
champ d'application ÉligibilitéAidePersonnaliséeLogement:
  exception l831_2_1 règle condition_logement_prêt sous condition
    selon ménage.logement.mode_occupation sous forme
      -- LocationAccession de propriété:
        # Le fait que cette exception ne soit valable qu'après le 1er janvier
        # 2018 nous a été confirmé le 25/05/2022 par DGALN/DHUP/FE4.
        propriété.prêt.date_signature >= |2018-01-01| et
        propriété.prêt.date_signature < |2020-01-01| et
        propriété.ancienneté_logement sous forme Ancien et
        logement_situé_commune_déséquilibre_l831_2
      -- AccessionPropriétéLocalUsageExclusifHabitation de propriété:
        propriété.prêt.date_signature >= |2018-01-01| et
        propriété.prêt.date_signature < |2020-01-01| et
        propriété.ancienneté_logement sous forme Ancien et
        logement_situé_commune_déséquilibre_l831_2
      -- n'importe quel: faux
  conséquence rempli
```

Un arrêté des ministres chargés du logement et du budget dresse la liste des
communes répondant aux conditions énoncées au deuxième alinéa.


###### Article L832-1 | LEGIARTI000038814876

L'aide personnalisée au logement est versée :

1° En cas de location, au bailleur du logement ;

2° En cas d'accession à la propriété, à l'établissement habilité à cette fin ;

3° En cas de résidence dans un logement-foyer, au gestionnaire de l'établissement.

En cas de mandat de gérance de logements, l'aide peut être versée au mandataire.
Dans des cas fixés par voie réglementaire, elle peut être versée au locataire ou au
propriétaire du logement.

```catala
# Ne touche ni à l'éligibilité ni au calcul du montant donc nous ne
# formaliserons pas pour l'instant.
```

###### Article L832-2 | LEGIARTI000038814874

Lorsque l'aide personnalisée au logement est versée au bailleur ou à l'établissement
habilité à cette fin, elle est déduite, par les soins de celui qui reçoit le versement,
du montant du loyer et des dépenses accessoires de logement ou de celui des charges
de remboursement.

Cette déduction doit être portée à la connaissance du bénéficiaire, locataire ou
propriétaire du logement.

La part de l'aide qui excède le montant du loyer et des charges récupérables est
versée à l'allocataire.

```catala
# Ne touche ni à l'éligibilité ni au calcul du montant donc nous ne
# formaliserons pas pour l'instant.
```

###### Article L832-3 | LEGIARTI000038814872

Le montant de l'aide personnalisée au logement est diminué, pour les bénéficiaires
concernés par la réduction de loyer de solidarité définie à l'article L. 442-2-1 ,
à hauteur d'une fraction de cette réduction comprise entre 90 % et 98 %. Cette fraction
est fixée par voie réglementaire.

```catala
champ d'application CalculAidePersonnaliséeLogementLocatif:
  définition traitement_aide_finale de aide_finale
  état réduction_loyer_solidarité
  égal à
    soit aide_finale égal à traitement_aide_finale de aide_finale dans
    si
      aide_finale - réduction_loyer_solidarité * fraction_l832_3 >= 0€
    alors
      aide_finale - réduction_loyer_solidarité * fraction_l832_3
    sinon 0€

  assertion fraction_l832_3 >= 90% et fraction_l832_3 <= 98%
```

###### Article L832-4 | LEGIARTI000038814870

Dans le cas prévu à l'article L. 824-3 , le versement reprend dans les conditions prévues
à l'article L. 832-1 .

```catala
# Ne touche ni à l'éligibilité ni au calcul du montant donc nous ne
# formaliserons pas pour l'instant.
```

#### Titre IV : Allocations de logement

##### Chapitre Ier : Champ d'application

```catala
champ d'application ÉligibilitéAllocationLogement:
  # Implicitement, l'éligibilité de l'allocation logement est conditionnée
  # à l'égibilité suivant les dispositions communes à toutes les aides
  # personnelles au logement. Comme il n'existe pas d'article de loi ou de
  # règlement rappelant ce fait, nous le traduisons en code ici.
  définition éligibilité état dispositions_communes égal à
    si
      (non éligibilité_commune.éligibilité) ou
      (non condition_accession_propriété)
    alors
      PasÉligible
    sinon
      # Valeur par défaut, sera réécrit à l'application de L841-2.
      TypeÉligibilitéAllocationLogement.AllocationLogementSociale
```

###### Article L841-1 | LEGIARTI000038814864

L'allocation de logement familiale est accordée :

1° Aux personnes qui perçoivent :

a) Soit les allocations familiales mentionnées au 2° de l'article L. 511-1 du code de la
sécurité sociale ;

b) Soit le complément familial mentionné au 3° du même article ;

c) Soit l'allocation d'éducation de l'enfant handicapé mentionnée au 5° du même article ;

d) Soit l'allocation de soutien familial mentionnée au 6° du même article ;

```catala
champ d'application ÉligibilitéAllocationLogement:
  règle l_841_1_1_applicable rempli

  étiquette l841_1_1 règle éligibilité_allocation_logement_familiale
  sous condition
    l_841_1_1_applicable et
    existe prestation parmi ménage.prestations_reçues tel que (
      prestation = PrestationReçue.AllocationsFamiliales ou
      prestation = PrestationReçue.ComplémentFamilial ou
      prestation = PrestationReçue.AllocationSoutienFamilial ou
      prestation = AllocationSoutienEnfantHandicapé
    )
  conséquence rempli
```

2° Aux ménages ou personnes qui, n'ayant pas droit à l'une des prestations énumérées au 1°,
ont un enfant à charge au sens de l'article L. 512-3 du code de la sécurité sociale ;

```catala
# "un enfant à charge": au moins un ou exactement un ?
# => OUI confirmé par DGALN/DHUP/FE4 le 25/05/2022
# Le cas de plus d'un enfant est déjà géré par le 1°
champ d'application ÉligibilitéAllocationLogement:
  règle l_841_1_2_applicable rempli

  étiquette l841_1_2 exception l841_1_1
  règle éligibilité_allocation_logement_familiale
  sous condition
    l_841_1_2_applicable et
    nombre de
      (personne_à_charge parmi ménage.personnes_à_charge tel que
       (selon personne_à_charge sous forme
        -- AutrePersonneÀCharge: faux
        -- EnfantÀCharge de enfant: prestations_familiales.droit_ouvert de
          EnfantPrestationsFamiliales {
          -- identifiant: enfant.identifiant
          -- obligation_scolaire: enfant.obligation_scolaire
          -- rémuneration_mensuelle: enfant.rémuneration_mensuelle
          -- date_de_naissance: enfant.date_de_naissance
          -- prise_en_charge: (selon enfant.situation_garde_alternée sous forme
            -- GardeAlternéeCoefficientPriseEnCharge:
              PriseEnChargeEnfant.GardeAlternéePartageAllocations
            -- PasDeGardeAlternée:
              PriseEnChargeEnfant.EffectiveEtPermanente)
          -- a_déjà_ouvert_droit_aux_allocations_familiales:
            enfant.a_déjà_ouvert_droit_aux_allocations_familiales
          -- bénéficie_titre_personnel_aide_personnelle_logement:
            enfant.
              bénéficie_titre_personnel_aide_personnelle_logement
          }))
    = 1
  conséquence rempli
```

3° Aux ménages qui n'ont pas d'enfant à charge, pendant une durée déterminée à compter du mariage ;

```catala
champ d'application ÉligibilitéAllocationLogement:
  étiquette l841_1_3 exception l841_1_2
  règle éligibilité_allocation_logement_familiale
  sous condition
    nombre de
      (personne_à_charge parmi ménage.personnes_à_charge tel que
       (selon personne_à_charge sous forme
        -- AutrePersonneÀCharge: faux
        -- EnfantÀCharge de enfant: non (prestations_familiales.droit_ouvert de
          EnfantPrestationsFamiliales {
          -- identifiant: enfant.identifiant
          -- obligation_scolaire: enfant.obligation_scolaire
          -- rémuneration_mensuelle: enfant.rémuneration_mensuelle
          -- date_de_naissance: enfant.date_de_naissance
          -- prise_en_charge: (selon enfant.situation_garde_alternée sous forme
            -- GardeAlternéeCoefficientPriseEnCharge:
              PriseEnChargeEnfant.GardeAlternéePartageAllocations
            -- PasDeGardeAlternée:
              PriseEnChargeEnfant.EffectiveEtPermanente)
          -- a_déjà_ouvert_droit_aux_allocations_familiales:
            enfant.a_déjà_ouvert_droit_aux_allocations_familiales
          -- bénéficie_titre_personnel_aide_personnelle_logement:
            enfant.
              bénéficie_titre_personnel_aide_personnelle_logement
          })))
      = 0
    et
    (selon ménage.situation_familiale sous forme
    # VERIF: opération ambiguë
    -- Mariés de date_mariage : date_courante <= date_mariage + durée_l841_1_3
    -- n'importe quel: faux)
  conséquence rempli
```

4° Aux ménages ou aux personnes qui ont à leur charge un ascendant vivant au foyer ayant dépassé
un âge déterminé ;

```catala
# Selon mail de DGALN/DHUP/FE4 du 06/07/2022, l'expression "âge déterminé" ici
# renvoie au 2° du R823-4 du même code.
champ d'application ÉligibilitéAllocationLogement:
  étiquette l841_1_4 exception l841_1_3
  règle éligibilité_allocation_logement_familiale
  sous condition
    nombre de (personne_à_charge parmi ménage.personnes_à_charge
      tel que éligibilité_commune.condition_2_r823_4 de personne_à_charge)
    >= 1
  conséquence rempli
```

5° Aux ménages ou personnes qui ont à leur charge un ascendant ou un descendant ou un collatéral
au deuxième ou au troisième degré vivant au foyer, dont l'incapacité permanente est au moins égale
à un pourcentage fixé par voie réglementaire ou qui présente, compte tenu de son handicap, une
restriction substantielle et durable dans l'accès à l'emploi au sens de l'article L. 821-2 du code
de la sécurité sociale reconnue par la commission des droits et de l'autonomie des personnes handicapées
prévue par l'article L. 146-9 du code de l'action sociale et des familles ;

```catala
champ d'application ÉligibilitéAllocationLogement:
  étiquette l841_1_5 exception l841_1_4
  règle éligibilité_allocation_logement_familiale
  sous condition
    (nombre de (personne_à_charge parmi ménage.personnes_à_charge tel que
      (selon personne_à_charge sous forme
      -- AutrePersonneÀCharge de parent:
        # Cela a l'air similaire au 3° du R823-4 code construction mais
        # ici le pourcentage est "fixé par voie réglementaire" au lieu d'être
        # fixé à 80%.
        # TODO juridique: vérifier si ce pourcentage fixé par voie réglementaire
        # est bien égal à 80%.
        parent.ascendant_descendant_collatéral_deuxième_troisième_degré et
        parent.incapacité_80_pourcent_ou_restriction_emploi
      -- EnfantÀCharge de enfant: faux
    ))) >= 1
  conséquence rempli
```

6° A la personne seule sans personne à charge à compter du premier jour du mois civil suivant le
quatrième mois de la grossesse et jusqu'au mois civil de la naissance de l'enfant.


```catala
champ d'application ÉligibilitéAllocationLogement:
  règle l_841_1_6_applicable rempli

  étiquette l841_1_6 exception l841_1_5
  règle éligibilité_allocation_logement_familiale
  sous condition
    l_841_1_6_applicable et
    selon ménage.situation_familiale sous forme
    -- Célibataire :
      nombre de ménage.personnes_à_charge = 0 et
      ménage.enfant_à_naître_après_quatrième_mois_grossesse
    -- n'importe quel : faux
  conséquence rempli
```

###### Article L841-2 | LEGIARTI000038814862

Les personnes ne bénéficiant pas de l'allocation de logement familiale ou de
l'aide personnalisée au logement peuvent prétendre au bénéfice de l'allocation
de logement sociale.

```catala
# Complément d'explications données par DGALN/DHUP/FE4 le 25/05/2022:
# "Il est exact de considérer que cet article détermine une hiérarchie pour
# l’accession aux aides au logement c'est-à-dire que :
# - Si un ménage est éligible à l’aide personnalisée au logement,
#   il bénéficie de l’aide personnalisée.
# - S’il n’est pas éligible à l’aide  personnalisée mais est
#   éligible à l’allocation de logement familiale, il bénéficie de cette
#   dernière.
# - S’il ne bénéficie ni de l’aide personnalisée, ni de l’allocation de
#   logement familiale, il peut prétendre à l’allocation de logement sociale."
# Dans notre code Catala, nous faisons cependant, et plus fidèlement au texte de
# loi, la différence entre éligibilité et bénéfice des différentes aides. Plus
# précisément, nous gardons l'option d'attribuer l'ALS/ALF préférentiellement
# à l'APL aux ménages éligibiles aux deux prestations, mais dont le montant
# d'ALF/ALS et supérieur au montant d'APL.
champ d'application ÉligibilitéAllocationLogement:
  étiquette base définition éligibilité état l841_2 égal à
    si éligibilité = PasÉligible ou bénéficie_aide_personnalisée_logement alors
      PasÉligible
    sinon (si éligibilité_allocation_logement_familiale alors
      TypeÉligibilitéAllocationLogement.AllocationLogementFamiliale
    sinon
      TypeÉligibilitéAllocationLogement.AllocationLogementSociale)

champ d'application CalculetteAidesAuLogement:
  # Le champ d'application CalculetteAidesAuLogement suppose un scenario type
  # formulaire de la CNAF qui va déterminer automatiquement la meilleure
  # aide au logement pour un ménage qui n'en bénéficie pas encore. Donc, dans
  # ce scénario, le ménage ne bénéficie pas encore de l'aide personnalisée
  # au logement et nous pouvons évaluer l'éligibilité à l'allocation logement
  # indépendamment de ce fait.
  définition
    éligibilité_allocation_logement.bénéficie_aide_personnalisée_logement
  égal à faux

  # Nous mettons ici la logique de notre interprétation de l'article L841-2
  # qui prend le maximum des deux types d'aide.
  définition éligibilité égal à
    éligibilité_aide_personnalisée_logement.éligibilité ou
    (selon éligibilité_allocation_logement.éligibilité sous forme
      -- PasÉligible: faux
      -- TypeÉligibilitéAllocationLogement.AllocationLogementFamiliale: vrai
      -- TypeÉligibilitéAllocationLogement.AllocationLogementSociale: vrai)

  # Pour chaque calcul d'aide potentielle à donner, on suppose que c'est
  # cette aide qui est donnée.
  définition calcul_aide_personnalisée_logement.type_aide égal à
    TypeAidesPersonnelleLogement.AidePersonnaliséeLogement
  définition calcul_allocation_logement.type_aide égal à
    (selon éligibilité_allocation_logement.éligibilité sous forme
      -- PasÉligible:
        # Valeur par défaut, ne sera pas utilisée
        TypeAidesPersonnelleLogement.AllocationLogementSociale
      -- TypeÉligibilitéAllocationLogement.AllocationLogementFamiliale:
        TypeAidesPersonnelleLogement.AllocationLogementFamiliale
      -- TypeÉligibilitéAllocationLogement.AllocationLogementSociale:
        TypeAidesPersonnelleLogement.AllocationLogementSociale)

  définition aide_finale_formule égal à
    si non éligibilité alors 0 € sinon si
      éligibilité_aide_personnalisée_logement.éligibilité et
      non (éligibilité_allocation_logement.éligibilité sous forme PasÉligible)
    alors
      (si calcul_aide_personnalisée_logement.traitement_aide_finale de
        calcul_aide_personnalisée_logement.aide_finale_formule >
          calcul_allocation_logement.traitement_aide_finale de
            calcul_allocation_logement.aide_finale_formule
      alors
        calcul_aide_personnalisée_logement.aide_finale_formule
      sinon
        calcul_allocation_logement.aide_finale_formule)
    sinon si éligibilité_aide_personnalisée_logement.éligibilité
    alors calcul_aide_personnalisée_logement.aide_finale_formule
    sinon calcul_allocation_logement.aide_finale_formule

  définition traitement_aide_finale de aide_finale égal à
    soit aide_finale_apl égal à
      calcul_aide_personnalisée_logement.traitement_aide_finale de aide_finale
    dans
    soit aide_finale_al égal à
      calcul_allocation_logement.traitement_aide_finale de aide_finale
    dans
    si non éligibilité alors
      aide_finale
    sinon si
        éligibilité_aide_personnalisée_logement.éligibilité et
        non (éligibilité_allocation_logement.éligibilité sous forme PasÉligible)
      alors
        (si aide_finale_apl > aide_finale_al
        alors aide_finale_apl
        sinon aide_finale_al)
      sinon si
          éligibilité_aide_personnalisée_logement.éligibilité
        alors
          aide_finale_apl
        sinon
          aide_finale_al
```

###### Article L841-3 | LEGIARTI000038814860

L'allocation de logement sociale est versée aux personnes hébergées dans les unités ou centres de
soins de longue durée mentionnés au 3° de l'article L. 162-22 du code de la sécurité sociale .

```catala
champ d'application ÉligibilitéAllocationLogement:
  exception accession_propriété
  # Nous considérons que L841-3 est une exception à L841-4 et non pas à
  # L841-2 (cas de base) car si L841-3 et L84164 étaient tous deux exceptions
  # de L841-2, alors il pourrait y avoir conflit entre ces deux exceptions.
  # Cependant le conflit est impossible puisqu'on ne peut pas à la fois
  # être dans une unité de soin longue durée et avoir acheté cette unité
  # de soin longue durée avec un prêt aidé par l'État.
  # Donc ici plutôt que d'écrire un invariant qui exclut ce cas de figure,
  # on préfère hiérarchiser entre elles les exceptions pour lever le conflit.
  définition éligibilité état l841_2 sous condition
    demandeur.personne_hébergée_centre_soin_l_L162_22_3_sécurité_sociale
  conséquence égal à
    TypeÉligibilitéAllocationLogement.AllocationLogementSociale
```

###### Article L841-4 | LEGIARTI000038814858

Aucune allocation de logement n'est due pour les prêts permettant d'accéder à la propriété de
l'habitation signés après le 31 décembre 2017.

```catala
champ d'application ÉligibilitéAllocationLogement:
  étiquette accession_propriété
  exception base définition éligibilité état l841_2 sous condition
    selon ménage.logement.mode_occupation sous forme
    -- AccessionPropriétéLocalUsageExclusifHabitation de propriétaire:
      propriétaire.prêt.date_signature > |2017-12-31|
    -- n'importe quel: faux
  conséquence égal à PasÉligible
```

###### Article L841-5 | LEGIARTI000038814856

L'allocation de logement sociale ne peut se cumuler avec une aide, quelle qu'en soit la dénomination,
ayant le même objet et due, sur le fondement d'une autre réglementation, à la même personne.

Toutefois, lorsque le montant ainsi dû est inférieur à celui de l'allocation de logement sociale à
laquelle les dispositions du présent livre ouvriraient droit, la différence est versée par l'organisme payeur.

```catala
# Pas pertinent pour l'éligibilité ou le calcul, plutôt un détail d'application
# d'une règle de non-cumul par la CAF qui dit qu'en cas de conflit entre l'ALS
# et une autre aide, il faut prendre l'aide de montant maximal.
```

##### Chapitre II : Modalités de liquidation et de versement des allocations de logement

###### Article L842-1 | LEGIARTI000038814852

L'allocation de logement est versée, sur leur demande, au prêteur ou au bailleur.

Le prêteur ou le bailleur déduit l'allocation du montant du loyer et des dépenses accessoires de logement
ou de celui des charges de remboursement. Il porte cette déduction à la connaissance de l'allocataire.
Il verse, le cas échéant, à l'allocataire la part de l'allocation de logement qui excède le montant du loyer
et des charges récupérables.

L'allocation ne peut être versée au bailleur que si le logement répond aux exigences prévues aux articles
L. 822-9 et L. 822-10 .

En cas de mandat de gérance de logements, l'allocation de logement peut être versée au mandataire.

```catala
# Pas pertinent pour le calcul ou l'éligibilité, nous ne formalisons pas pour
# l'instant.
```

###### Article L842-2 | LEGIARTI000038814849

Dans le cas prévu à l'article L. 824-3 , le versement reprend au profit du bailleur, sauf dans le cas prévu
au troisième alinéa de l'article L. 842-1 ou en cas de refus de ce dernier.

```catala
# Pas pertinent pour le calcul ou l'éligibilité, nous ne formalisons pas pour
# l'instant.
```

#### Titre V : Contrôles, lutte contre la fraude et sanctions

```catala
# Rien à coder.
```

#### Titre VI : Dispositions particulières à l'outre-mer

####### Article L860-1 | LEGIARTI000038814799

Les dispositions du présent livre s'appliquent, de plein droit, en Guadeloupe,
en Guyane, à la Martinique, à La Réunion et à Mayotte, sous les réserves et dans
les conditions énoncées par les dispositions prévues par le chapitre Ier du
présent titre.

####### Article L860-2 | LEGIARTI000038814797

Les dispositions du présent livre s'appliquent, de plein droit, à
Saint-Barthélemy et à Saint-Martin, sous les réserves et dans les conditions
énoncées au chapitre II du présent titre.

####### Article L860-3 | LEGIARTI000038814795

I. - Les dispositions du titre III du présent livre ne sont pas applicables à
Saint-Pierre-et-Miquelon.

```catala
# Les habitants de Saint-Pierre et Miquelon n'ont pas droit à l'APL, seulement
# à l'allocation logement.
champ d'application ÉligibilitéAidePersonnaliséeLogement:
  exception règle éligibilité sous condition
    date_courante >= |2021-01-01| et
    ménage.résidence sous forme SaintPierreEtMiquelon
  conséquence non rempli
```

II. - Les autres dispositions du présent livre s'appliquent, de plein droit,
à Saint-Pierre-et-Miquelon, sous les réserves et dans les conditions énoncées
au chapitre III du présent titre.

```catala
# Rien à coder pour l'instant, on verra au chapitre III du présent titre.
```

NOTA :

Aux termes du III de l'article 23 de l'ordonnance n° 2019-770 du 17 juillet
2019, le II de l’article L. 860-3 du code de la construction et de l’habitation,
annexé à la présente ordonnance, entre en vigueur le 1er janvier 2021.

##### Chapitre Ier : Guadeloupe, Guyane, Martinique, La Réunion et Mayotte

###### Section 1 : Fonds national d'aide au logement

```catala
# Rien à coder.
```
###### Section 2 : Dispositions communes aux aides personnelles au logement

####### Article L861-4 | LEGIARTI000038814781

Les articles L. 821-4 et L. 823-8 , relatifs aux primes de déménagement,
ne sont pas applicables à Mayotte.

```catala
champ d'application ÉligibilitéPrimeDeDéménagement:
  exception règle éligibilité_logement sous condition
    ménage.résidence sous forme Mayotte
  conséquence non rempli
```

####### Article L861-5 | LEGIARTI000044405110

Pour l'application à Mayotte des dispositions du titre II :

1° Au 2° du I de l'article L. 822-2, les mots : " les deux premiers alinéas de
l'article L. 512-2 du code de la sécurité sociale " sont remplacés par les
mots : " l'article 4 de l'ordonnance n° 2002-149 du 7 février 2002 relative à
l'extension et la généralisation des prestations familiales et à la protection
sociale dans la collectivité départementale de Mayotte " ;

```catala
champ d'application ÉligibilitéAidesPersonnelleLogement:
  # Ici on utilise le mécanisme d'exception pour remplacer la définition
  # de l'article L822-2 et ainsi adapter le droit pour son application à
  # Mayotte.
  exception définition condition_nationalité sous condition
    ménage.résidence sous forme Mayotte
  conséquence égal à
    selon demandeur.nationalité sous forme
    -- Française: faux
    -- Étrangère de conditions:
      conditions.satisfait_art_4_ordonnance_2002_mayotte
```

2° A l'article L. 822-5 , les mots : " prévue à l'article L. 821-1 du code de
la sécurité sociale " sont remplacés par les mots : " prévue à l' article 35
de l'ordonnance n° 2002-411 du 27 mars 2002 relative à la protection sanitaire
et sociale à Mayotte " et les mots : " prévue à l'article L. 541-1 du même
code " sont remplacés par les mots : " prévu par l'article 10-1 de l'ordonnance
n° 2002-149 du 7 février 2002 relative à l'extension et la généralisation des
prestations familiales et à la protection sociale dans la collectivité
départementale de Mayotte " ;

```catala
# Le code dans L822-5 fait référence à l'allocation adultes handicapés ainsi
# qu'à l'allocation d'éducation de l'enfant handicapé qui sont définies pour
# Mayotte dans d'autres textes d'où le 2° ci-dessus; rien à changer.
```

3° L'article L. 822-10 est remplacé par les dispositions suivantes :

" Art. L. 822-10.-L'attribution d'une aide personnelle au logement est
subordonnée au respect de conditions de peuplement des logements. Si un
logement devient surpeuplé, du fait de l'arrivée au foyer d'un conjoint ou
d'un ascendant à charge, l'aide est maintenue pendant une durée déterminée. "
Ces conditions de peuplement et la durée du maintien de l'aide sont fixées
par voie réglementaire. " ;

```catala
# Cet article n'affecte pas l'éligibilité et le montant du ménage aux aides
# au logement donc nous le considérons hors du champ de notre calculette.
```

4° Le septième alinéa de l'article L. 823-1 n'est pas applicable ;

```catala
champ d'application ÉligibilitéAidesPersonnelleLogement:
  exception règle septième_alinéa_l823_1_applicable sous condition
    ménage.résidence sous forme Mayotte
  conséquence non rempli
```

5° A l'article L. 823-9 , les mots : " Les articles L. 161-1-5 et L. 553-2 du
code de la sécurité sociale " sont remplacés par les mots : " Le deuxième
alinéa de l'article 20 de l'ordonnance n° 2002-411 du 27 mars 2002 relative à
la protection sanitaire et sociale à Mayotte et l' article 13 de l'ordonnance
n° 2002-149 du 7 février 2002 relative à l'extension et la généralisation des
prestations familiales et à la protection sociale dans la collectivité
territoriale de Mayotte ".

```catala
# L'article L823-9 concerne le recouvrement des indus d'aides au logement
# qui est en dehors du champ de notre calculette.
```

NOTA :

Conformément à l'article 20 de l'ordonnance n° 2020-1733 du 16 décembre 2020,
ces dispositions entrent en vigueur le 1er mai 2021.

###### Section 3 : Aide personnalisée au logement

####### Article L861-5-1 | LEGIARTI000044899551

Pour l'application de l'article L. 831-1 en Guadeloupe, en Guyane, en Martinique,
à La Réunion et à Mayotte, le 5° est ainsi rédigé :

“5° Logements-foyers, dès lors qu'ils font l'objet d'une convention dont les
conditions sont fixées par voie réglementaire ;”.

```catala
champ d'application ÉligibilitéAidePersonnaliséeLogement sous condition
  (selon ménage.résidence sous forme
  -- Guadeloupe: vrai
  -- Guyane: vrai
  -- Martinique: vrai
  -- LaRéunion: vrai
  -- Mayotte: vrai
  -- n'importe quel: faux):
  exception l831_1_alinea_5
  règle condition_logement_bailleur sous condition
    selon ménage.logement.mode_occupation sous forme
      -- RésidentLogementFoyer de location:
        location.conventionné_selon_règles_drom
      -- n'importe quel: faux
  conséquence rempli
```

###### Section 4 : Allocations de logement

####### Article L861-6 | LEGIARTI000038814775

Pour l'application en Guadeloupe, en Guyane, en Martinique, à La Réunion et à
Mayotte des dispositions du titre IV :

1° L'article L. 841-1 est ainsi modifié :

a) Les 1°, 2° et 6° sont supprimés et les 3°, 4° et 5° deviennent les 1°, 2° et
3° ;

```catala
champ d'application ÉligibilitéAllocationLogement sous condition
  (selon ménage.résidence sous forme
  -- Guadeloupe: vrai
  -- Guyane: vrai
  -- Martinique: vrai
  -- LaRéunion: vrai
  -- Mayotte: vrai
  -- n'importe quel: faux):

  règle l_841_1_1_applicable non rempli
  règle l_841_1_2_applicable non rempli
  règle l_841_1_6_applicable non rempli
  # Nous désactivons seulement les 1°, 2° et 6° sans faire la renumérotation
  # qui semble ne concerner que les b) et c) suivants (à prendre en compte
  # dans le code correspondant).
```

b) Au 3° ainsi renuméroté, les mots : " l' article L. 821-2 du code de la
sécurité sociale " sont remplacés par les mots : " l' article 35 de l'ordonnance
n° 2002-411 du 27 mars 2002 relative à la protection sanitaire et sociale à
Mayotte " ;

```catala
# Ici on se réfère à des règles propres à Mayotte pour évaluer l'incapacité à
# travailler ou la restriction d'emploi, mais cette incapacité ou restriction
# continue à fonctionner de la même manière qu'en métropole en ce qui
# concerne l'éligibilité à l'allocation logement, donc rien à coder pour nous.
```

c) L'article est complété par quatre alinéas ainsi rédigés :

" 4° Aux personnes qui ont au moins un enfant à charge au sens de l'article
L. 512-3 du code de la sécurité sociale ;

```catala
# Ressemble au 2° de L841-1 mais subtilement différent.
champ d'application ÉligibilitéAllocationLogement sous condition
  (selon ménage.résidence sous forme
  -- Guadeloupe: vrai
  -- Guyane: vrai
  -- Martinique: vrai
  -- LaRéunion: vrai
  -- Mayotte: vrai
  -- n'importe quel: faux):
  étiquette l841_1_4_mayotte exception l841_1_6
  règle éligibilité_allocation_logement_familiale
  sous condition
    nombre de
      (personne_à_charge parmi ménage.personnes_à_charge tel que
       (selon personne_à_charge sous forme
        -- AutrePersonneÀCharge: faux
        -- EnfantÀCharge de enfant: prestations_familiales.droit_ouvert de
          EnfantPrestationsFamiliales {
          -- identifiant: enfant.identifiant
          -- obligation_scolaire: enfant.obligation_scolaire
          -- rémuneration_mensuelle: enfant.rémuneration_mensuelle
          -- date_de_naissance: enfant.date_de_naissance
          -- prise_en_charge: (selon enfant.situation_garde_alternée sous forme
            -- GardeAlternéeCoefficientPriseEnCharge:
              PriseEnChargeEnfant.GardeAlternéePartageAllocations
            -- PasDeGardeAlternée:
              PriseEnChargeEnfant.EffectiveEtPermanente)
          -- a_déjà_ouvert_droit_aux_allocations_familiales:
            enfant.a_déjà_ouvert_droit_aux_allocations_familiales
          -- bénéficie_titre_personnel_aide_personnelle_logement:
            enfant.
              bénéficie_titre_personnel_aide_personnelle_logement
          }))
    >= 1
  conséquence rempli
```

" 5° Aux personnes mentionnées aux articles L. 781-8 et L. 781-46 du code rural
et de la pêche maritime ;

```catala
# Les personnes mentionnées aux articles L. 781-8 et L. 781-46 du code rural
# et de la pêche maritime sont les non-salariés agricoles.
champ d'application ÉligibilitéAllocationLogement sous condition
  (selon ménage.résidence sous forme
  -- Guadeloupe: vrai
  -- Guyane: vrai
  -- Martinique: vrai
  -- LaRéunion: vrai
  -- Mayotte: vrai
  -- n'importe quel: faux):
  étiquette l841_1_5_mayotte exception l841_1_4_mayotte
  règle éligibilité_allocation_logement_familiale
  sous condition
    demandeur.est_non_salarié_agricole_l781_8_l_781_46_code_rural
  conséquence rempli
```

" 6° A Mayotte, lorsqu'un bénéficiaire est marié sous le régime du statut civil
de droit local, dans sa version antérieure à l' ordonnance n° 2010-590 du 3 juin
2010 portant dispositions relatives au statut civil de droit local applicable à
Mayotte et aux juridictions compétentes pour en connaître, seule sa première
épouse est prise en compte au titre de ses droits. Ses autres épouses peuvent
faire, le cas échéant, une demande à titre personnel ; dans ce cas, les
ressources de leur mari sont prises en compte pour le droit et le calcul de
l'allocation de logement sociale.

```catala
# Cet article concerne le traitement particulier de la polygamie coutumière à
# Mayotte. Le traitement de ce cas ne nécessite pas tant de modification à notre
# calculette des aides au logement mais donne plutôt des instructions sur
# comment remplir le formulaire dans cette situation. Nous choisissons de
# ne rien coder ici.
```

" L'âge limite pour l'ouverture du droit à cette allocation est fixé par voie
réglementaire pour tout enfant dont la rémunération n'excède pas le plafond
mentionné au 2° de l'article L. 512-3 du code de la sécurité sociale à condition
qu'il poursuive des études, ou qu'il soit placé en apprentissage ou en stage de
formation professionnelle au sens de la sixième partie du code du travail, ou
qu'il se trouve, par suite d'infirmité ou de maladie chronique, dans
l'impossibilité constatée de se livrer à une activité professionnelle. " ;

```catala
# L'analyse juridique de cet alinéa montre qu'ici nous rajoutons une condition
# plus restrictive d'ouverture des droits pour les enfants à charge compris
# entre la fin de l'obligation scolaire et l'âge limite de 21 ans défini à
# l'article R823-4 : il faut en effet que l'enfant soit en études ou
# apprentissage, etc.
champ d'application ÉligibilitéAidesPersonnelleLogement sous condition
  (selon ménage.résidence sous forme
  -- Guadeloupe: vrai
  -- Guyane: vrai
  -- Martinique: vrai
  -- LaRéunion: vrai
  -- Mayotte: vrai
  -- n'importe quel: faux):

  exception r823_4_enfants
  règle prise_en_compte_personne_à_charge de personne_à_charge sous condition
    selon personne_à_charge sous forme
    -- EnfantÀCharge de enfant:
      enfant.obligation_scolaire sous forme Après et
      (enfant.date_de_naissance + prestations_familiales.âge_l512_3_2 >
        date_courante) et
      enfant.études_apprentissage_stage_formation_pro_impossibilité_travail
    -- AutrePersonneÀCharge: faux
  conséquence non rempli
```

2° L'article L. 841-4 est remplacé par les dispositions suivantes :

" Art. L. 841-4.-L'allocation de logement n'est pas due pour les prêts permettant
d'accéder à la propriété de l'habitation signés après le 31 décembre 2017 ou,
par exception, après le 31 décembre 2019 lorsque le logement a fait l'objet d'une
décision favorable de financement, prise avant le 31 décembre 2018, du
représentant de l'Etat en Guyane, en Martinique, en Guadeloupe, à La Réunion et à
Mayotte. "

```catala
champ d'application ÉligibilitéAllocationLogement sous condition
  (selon ménage.résidence sous forme
  -- Guadeloupe: vrai
  -- Guyane: vrai
  -- Martinique: vrai
  -- LaRéunion: vrai
  -- Mayotte: vrai
  -- n'importe quel: faux):
  étiquette accession_propriété_outre_mer exception accession_propriété
  définition éligibilité état l841_2 sous condition
    selon ménage.logement.mode_occupation sous forme
    -- AccessionPropriétéLocalUsageExclusifHabitation de propriétaire:
      propriétaire.prêt.date_signature > |2017-12-31| ou (
      propriétaire.prêt.date_signature > |2019-12-31| et
      selon
        propriétaire.prêt.accord_financement_représentant_État_outre_mer
      sous forme
      -- Accord de date_accord: date_accord <= |2018-12-31|
      -- PasdAccord: faux )
    -- n'importe quel: faux
  conséquence égal à PasÉligible
```

####### Article L816-7 | LEGIARTI000038814773

Les articles L. 843-1 à L. 843-7 relatifs à la procédure de conservation de
l'allocation de logement par l'organisme payeur, lorsque le logement ne constitue
pas un logement décent, ne sont pas applicables à Mayotte.

```catala
# Hors du champ de notre calculette.
```

####### Article L816-8 | LEGIARTI000038814771

A Mayotte, le bénéfice de l'allocation de logement n'est pas ouvert aux magistrats
et aux fonctionnaires civils et militaires de l'Etat dont le centre des intérêts
matériels et familiaux est situé hors de Mayotte.

```catala
champ d'application ÉligibilitéAllocationLogement sous condition
  ménage.résidence sous forme Mayotte:
  exception accession_propriété_outre_mer
  définition éligibilité état l841_2 sous condition
    demandeur.
      magistrat_fonctionnaire_centre_intérêts_matériels_familiaux_hors_mayotte
  conséquence égal à PasÉligible
```
###### Section V : Contrôles, luttes contre la fraude et sanctions

```catala
# Rien à coder.
```

##### Chapitre II : Saint-Barthélemy et Saint-Martin

###### Section 1 : Fonds national d'aide au logement

```catala
# Rien à coder.
```

###### Section 2 : Dispositions communes aux aides personnelles au logement

####### Article L862-2 | LEGIARTI000038814757

Pour l'application à Saint-Barthélemy et à Saint-Martin des dispositions du titre II :

1° L'article L. 821-8 est remplacé par les dispositions suivantes :
" Art. L. 821-8.-Les aides personnelles au logement ne sont pas comprises dans les
ressources prises en compte en vue de l'attribution des prestations de vieillesse,
de prestations familiales, des prestations d'aide sociale ou de l'allocation aux
adultes handicapés. " ;

```catala
# L'article L821-8 est hors du champ de notre calculette pour l'instant
# (détermination des ressources prises en compte).
```

2° A l'article L. 822-8, la référence à l'article 964 du code général des impôts est
remplacée par la référence aux dispositions applicables localement ayant le même
objet ;

```catala
# Fait la correspondance avec le droit local mais ne change pas le calcul, on ne
# code donc rien ici.
```

3° L'article L. 822-9 est remplacé par les dispositions suivantes :
" Art. L. 822-9.-L'aide n'est due qu'aux personnes dont le logement répond à des
conditions de décence définies par voie réglementaire. " ;

```catala
# Fait la correspondance avec le droit local mais ne change pas le calcul, on ne
# code donc rien ici.
```

4° L'article L. 823-3 est remplacé par les dispositions suivantes :

" Art. L. 823-3.-Sont assimilées aux loyers :
" 1° Les mensualités acquittées au titre des charges de remboursement des prêts
contractés pour l'acquisition du logement ou son amélioration ;
" 2° La redevance versée dans le cadre de contrats de location-accession portant sur
des immeubles à usage d'habitation ou à usage professionnel et d'habitation, en
contrepartie du droit de l'accédant à la jouissance du logement et de son droit
personnel au transfert de propriété du bien ;
" 3° La rémunération de l'opérateur chargé de toutes les missions concourant au
redressement de la copropriété, notamment la maîtrise d'ouvrage d'un programme de
travaux et la mise au point du financement de l'opération, désigné par
l'administrateur provisoire nommé par le juge, lorsque la situation financière d'une
copropriété ne permet pas de réaliser les travaux nécessaires à la conservation et
la mise en sécurité de l'immeuble, la protection des occupants, la préservation de
leur santé et la réduction des charges de copropriété permettant son redressement
financier ;
" 4° L'indemnité d'occupation versée à l'expropriant ;
" 5° La redevance versée à l'opérateur en charge des parties communes expropriées ;
" 6° L'indemnité, mentionnée au 4° de l'article L. 442-1 du code de l'action sociale
et des familles , représentative de la mise à disposition de la ou des pièces
réservées à la personne accueillie. " ;

```catala
# Fait la correspondance avec le droit local mais ne change pas le calcul, on ne
# code donc rien ici.
```

5° Le deuxième alinéa de l'article L. 823-4 est remplacé par les dispositions
suivantes : " Les paramètres suivants sont indexés sur l'évolution de l'indice de
référence des loyers défini soit par les dispositions applicables localement ayant
le même objet que celles de l' article 17-1 de la loi n° 89-462 du 6 juillet 1989
tendant à améliorer les rapports locatifs et portant modification de la loi
n° 86-1290 du 23 décembre 1986 , soit, à défaut d'indice spécifique à la
collectivité, par les dispositions en vigueur en métropole : ".

```catala
# Fait la correspondance avec le droit local mais ne change pas le calcul, on ne
# code donc rien ici.
```

###### Section 3 : Allocations de logement

##### Article L862-3 | LEGIARTI000038814753

Pour l'application à Saint-Barthélemy et à Saint-Martin des dispositions du titre
IV :

1° L'article L. 841-1 est ainsi modifié :

a) Les 1°, 2° et 6° sont abrogés et les 2°, 3° et 5° deviennent respectivement,
les 1°, 2° et 3° ;

```catala
# Ici le a) ne fait pas sens car le 2° est à la fois abrogé et renuméroté...
# Nous faisons abstraction de ce contresens et raisonnons par analogie
# en considérant qu'on fait ici la même chose que dans le a) de L816-6.
champ d'application ÉligibilitéAllocationLogement sous condition
  (selon ménage.résidence sous forme
  -- SaintBarthélemy: vrai
  -- SaintMartin: vrai
  -- n'importe quel: faux):

  règle l_841_1_1_applicable non rempli
  règle l_841_1_2_applicable non rempli
  règle l_841_1_6_applicable non rempli
  # Nous désactivons seulement les 1°, 2° et 6° sans faire la renumérotation
  # qui semble ne concerner que les b) et c) suivants (à prendre en compte
  # dans le code correspondant).
```

b) L'article est complété par trois alinéas ainsi rédigés :
" 4° Aux personnes qui ont au moins un enfant à charge au sens de l' article
L. 512-3 du code de la sécurité sociale ;


```catala
# Ressemble au 2° de L841-1 mais subtilement différent.
champ d'application ÉligibilitéAllocationLogement sous condition
  (selon ménage.résidence sous forme
  -- SaintBarthélemy: vrai
  -- SaintMartin: vrai
  -- n'importe quel: faux):

  étiquette l841_1_4_saint_barth_martin exception l841_1_6
  règle éligibilité_allocation_logement_familiale
  sous condition
    nombre de
      (personne_à_charge parmi ménage.personnes_à_charge tel que
       (selon personne_à_charge sous forme
        -- AutrePersonneÀCharge: faux
        -- EnfantÀCharge de enfant: prestations_familiales.droit_ouvert de
          EnfantPrestationsFamiliales {
          -- identifiant: enfant.identifiant
          -- obligation_scolaire: enfant.obligation_scolaire
          -- rémuneration_mensuelle: enfant.rémuneration_mensuelle
          -- date_de_naissance: enfant.date_de_naissance
          -- prise_en_charge: (selon enfant.situation_garde_alternée sous forme
            -- GardeAlternéeCoefficientPriseEnCharge:
              PriseEnChargeEnfant.GardeAlternéePartageAllocations
            -- PasDeGardeAlternée:
              PriseEnChargeEnfant.EffectiveEtPermanente)
          -- a_déjà_ouvert_droit_aux_allocations_familiales:
            enfant.a_déjà_ouvert_droit_aux_allocations_familiales
          -- bénéficie_titre_personnel_aide_personnelle_logement:
            enfant.
              bénéficie_titre_personnel_aide_personnelle_logement
          }))
    >= 1
  conséquence rempli
```

" 5° Aux personnes mentionnées aux articles L. 781-8 et L. 781-46 du code rural et
de la pêche maritime.

```catala
# Les personnes mentionnées aux articles L. 781-8 et L. 781-46 du code rural
# et de la pêche maritime sont les non-salariés agricoles.
champ d'application ÉligibilitéAllocationLogement sous condition
  (selon ménage.résidence sous forme
  -- SaintBarthélemy: vrai
  -- SaintMartin: vrai
  -- n'importe quel: faux):

  étiquette l841_1_5_saint_barth_martin exception l841_1_4_saint_barth_martin
  règle éligibilité_allocation_logement_familiale
  sous condition
    demandeur.est_non_salarié_agricole_l781_8_l_781_46_code_rural
  conséquence rempli
```

" L'âge limite pour l'ouverture du droit à cette allocation est fixé par voie
réglementaire pour tout enfant dont la rémunération n'excède pas le plafond
mentionné au 2° de l'article L. 512-3 du code de la sécurité sociale à condition
qu'il poursuive des études, ou qu'il soit placé en apprentissage ou en stage de
formation professionnelle au sens de la sixième partie du code du travail, ou
qu'il se trouve, par suite d'infirmité ou de maladie chronique, dans l'impossibilité
constatée de se livrer à une activité professionnelle. " ;

```catala
# L'analyse juridique de cet alinéa montre qu'ici nous rajoutons une condition
# plus restrictive d'ouverture des droits pour les enfants à charge compris
# entre la fin de l'obligation scolaire et l'âge limite de 21 ans défini à
# l'article R823-4 : il faut en effet que l'enfant soit en études ou
# apprentissage, etc.
champ d'application ÉligibilitéAidesPersonnelleLogement sous condition
  (selon ménage.résidence sous forme
  -- SaintBarthélemy: vrai
  -- SaintMartin: vrai
  -- n'importe quel: faux):

  exception r823_4_enfants
  règle prise_en_compte_personne_à_charge de personne_à_charge sous condition
    selon personne_à_charge sous forme
    -- EnfantÀCharge de enfant:
      enfant.obligation_scolaire sous forme Après et
      (enfant.date_de_naissance + prestations_familiales.âge_l512_3_2 >
        date_courante) et
      enfant.études_apprentissage_stage_formation_pro_impossibilité_travail
    -- AutrePersonneÀCharge: faux
  conséquence non rempli
```

2° A l'article L. 843-7 , les mots : " définies au 1° de l'article L. 365-1 " sont
remplacés par les mots : " d'opérations d'acquisition, de construction ou de
réhabilitation de logements ou de structures d'hébergement en tant que propriétaire
ou preneur de bail à construction, emphytéotique ou de bail à réhabilitation ".

```catala
# Fait la correspondance avec le droit local mais ne change pas le calcul, on ne
# code donc rien ici.
```

###### Section 4 : Contrôles, lutte contre la fraude et sanctions

```catala
# Rien à coder.
```

##### Chapitre III : Saint-Pierre-et-Miquelon

###### Section 1 : Fonds national d'aide au logement

```catala
# Rien à coder.
```

###### Section 2 : Dispositions communes aux aides applicables à Saint-Pierre-et-Miquelon

####### Article L863-2 | LEGIARTI000038814739

Les articles L. 821-4 et L. 823-8 relatifs aux primes de déménagement ne sont pas
applicables à Saint-Pierre-et-Miquelon.

```catala
champ d'application ÉligibilitéPrimeDeDéménagement sous condition
  date_courante >= |2022-01-01| :
  exception règle éligibilité_logement sous condition
    ménage.résidence sous forme SaintPierreEtMiquelon
  conséquence non rempli
```

NOTA : Aux termes du IV de l'article 23 de l'ordonnance n° 2019-770 du 17 juillet
2019, les articles L. 863-1 à L. 863-5 du code de la construction et de l’habitation,
annexés à la présente ordonnance, s’appliquent aux contributions et aux prestations
dues à compter du 1er janvier 2022.

####### Article L863-3 | LEGIARTI000038814737

Pour l'application à Saint-Pierre-et-Miquelon des dispositions du titre II :

1° L'article L. 821-8 est remplacé par les dispositions suivantes :

" Art. L. 821-8.-Les aides personnelles au logement ne sont pas comprises dans les
ressources prises en compte en vue de l'attribution des prestations de vieillesse,
de prestations familiales, des prestations d'aide sociale ou de l'allocation aux
adultes handicapés. " ;

```catala
# Prise en compte des ressources pas encore formalisée en Catala donc rien à
# coder ici pour le moment.
```

2° A l'article L. 822-8 , la référence à l' article 964 du code général des impôts
est remplacée par la référence aux dispositions applicables localement ayant le même
objet ;

```catala
# Fait la correspondance avec le droit local mais ne change pas le calcul, on ne
# code donc rien ici.
```

3° L'article L. 822-9 est remplacé par les dispositions suivantes :

" Art. L. 822-9.-L'aide n'est due qu'aux personnes dont le logement répond à des
conditions de décence fixées par voie réglementaire. " ;

```catala
# Fait la correspondance avec le droit local mais ne change pas le calcul, on ne
# code donc rien ici.
```

4° L'article L. 823-3 est remplacé par les dispositions suivantes :

" Art. L. 823-3.-Sont assimilées aux loyers :
" 1° Les mensualités acquittées au titre des charges de remboursement des prêts contractés pour l'acquisition du
logement ou son amélioration ;
" 2° La redevance versée dans le cadre de contrats de location-accession portant sur
des immeubles à usage d'habitation ou à usage professionnel et d'habitation en
contrepartie du droit de l'accédant à la jouissance du logement et de son droit
personnel au transfert de propriété du bien ;
" 3° La rémunération de l'opérateur chargé de toutes les missions concourant au
redressement de la copropriété, notamment la maîtrise d'ouvrage d'un programme de
travaux et la mise au point du financement de l'opération, désigné par
l'administrateur provisoire nommé par le juge lorsque la situation financière d'une
copropriété ne permet pas de réaliser les travaux nécessaires à la conservation et
la mise en sécurité de l'immeuble, la protection des occupants, la préservation de
leur santé et la réduction des charges de copropriété permettant son redressement
financier ;
" 4° L'indemnité d'occupation versée à l'expropriant ;
" 5° La redevance versée à l'opérateur en charge des parties communes expropriées ;
" 6° L'indemnité, mentionnée au 4° de l'article L. 442-1 du code de l'action sociale
et des familles , représentative de la mise à disposition de la ou des pièces
réservées à la personne accueillie. " ;

```catala
# Fait la correspondance avec le droit local mais ne change pas le calcul, on ne
# code donc rien ici.
```

5° Le deuxième alinéa de l'article L. 823-4 est remplacé par les dispositions
suivantes :
" Les paramètres suivants sont indexés sur l'évolution de l'indice de référence des
loyers défini soit par les dispositions applicables localement ayant le même objet
que celles de l' article 17-1 de la loi n° 89-462 du 6 juillet 1989 tendant à
améliorer les rapports locatifs et portant modification de la loi n° 86-1290 du
23 décembre 1986 , soit, à défaut d'indice spécifique à la collectivité, par les
dispositions en vigueur en métropole : ".

```catala
# Fait la correspondance avec le droit local mais ne change pas le calcul, on ne
# code donc rien ici.
```

NOTA : Aux termes du IV de l'article 23 de l'ordonnance n° 2019-770 du 17 juillet
2019, les articles L. 863-1 à L. 863-5 du code de la construction et de l’habitation,
annexés à la présente ordonnance, s’appliquent aux contributions et aux prestations
dues à compter du 1er janvier 2022.

###### Section 3 : Allocations de logement

####### Article L863-4 | LEGIARTI000038814733

Les articles L. 843-1 à L. 843-7 relatifs à la procédure de conservation de
l'allocation de logement par l'organisme payeur lorsque le logement ne constitue pas
un logement décent ne sont pas applicables à Saint-Pierre-et-Miquelon.

```catala
# Hors du champ de notre calculette.
```


NOTA : Aux termes du IV de l'article 23 de l'ordonnance n° 2019-770 du 17 juillet
2019, les articles L. 863-1 à L. 863-5 du code de la construction et de l’habitation,
annexés à la présente ordonnance, s’appliquent aux contributions et aux prestations
dues à compter du 1er janvier 2022.

###### Section 4 : Contrôles, lutte contre la fraude et sanctions

```catala
# Rien à coder.
```
