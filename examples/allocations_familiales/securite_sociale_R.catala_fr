### Partie réglementaire - Décrets en Conseil d'Etat

#### Livre 5 : Prestations familiales et prestations assimilées

##### Titre 1 : Champ d'application - Généralités

###### Chapitre 2 : Champ d'application.

#### [Article R512-2|LEGIARTI000006750602]

Les enfants ouvrent droit aux prestations familiales jusqu'à l'âge de vingt
ans sous réserve que leur rémunération n'excède pas le plafond fixé au deuxième
alinéa du présent article.

```catala
champ d'application PrestationsFamiliales :
  définition âge_l512_3_2 égal à 20
```

Le plafond de rémunération mentionné au 2° de l'article L. 512-3 est égal, pour
un mois, à 55 % du salaire minimum interprofessionnel de croissance défini aux
articles L. 141-1 à L. 141-9 du code du travail, multiplié par 169.


```catala
champ d'application PrestationsFamiliales :
  définition plafond_l512_3_2 égal à
   (smic.brut_horaire *€ 55 %) *€ 169,0
```

Pour ceux des enfants qui bénéficient d'avantages en nature, l'évaluation de ces
avantages devra être faite suivant les barèmes fixés pour l'application de la
législation sur les assurances sociales.


```catala
# Le programme ne tient pas en compte des avantages en nature
```

##### Titre 2 : Prestations générales d'entretien

###### Chapitre 1er : Allocations familiales

#### [Article R521-1|LEGIARTI000018735853]
L'âge mentionné au premier alinéa de l'article L. 521-3 à partir duquel les
enfants ouvrent droit à la majoration des allocations familiales est fixé à
14 ans.

```catala
champ d'application AllocationsFamiliales :
  étiquette cas_base_âge_minimum_alinéa_1_l521_3
  définition âge_minimum_alinéa_1_l521_3 de enfant égal à 14
```

Le nombre minimum d'enfants à charge, mentionné au deuxième alinéa de l'article
L. 521-3 ouvrant droit à ladite majoration pour chaque enfant est fixé à trois.

```catala
champ d'application AllocationsFamiliales :
  définition nombre_enfants_alinéa_2_l521_3 égal à 3
```

NOTA : Décret n° 2008-409 du 28 avril 2008 JORF du 29 avril 2008 art. 2 : Les
modifications induites par le décret n° 2008-409 s'appliquent aux enfants dont
le onzième anniversaire est postérieur au 30 avril 2008.

```catala
# Notons ici un champ d'application différent, correspondant à une ancienne
# version du corpus législatif dont un morceau s'applique encore. Nous avons
# choisi de montrer ce vieux champ d'application ici plutôt qu'à côté du texte
# du décret de 2008 pour des raisons de place seulement.
champ d'application AllocationFamilialesAvril2008:
  # Âge limite avant décret n° 2008-409 du 28 avril 2008
  définition âge_minimum_alinéa_1_l521_3 égal à 16

champ d'application AllocationsFamiliales :
  étiquette decret_2008_âge_minimum_alinéa_1_l521_3
  exception cas_base_âge_minimum_alinéa_1_l521_3
  définition âge_minimum_alinéa_1_l521_3 de enfant
  sous condition (enfant.date_de_naissance +@ 11 an <=@ |2008-04-30|)
  conséquence égal à
    version_avril_2008.âge_minimum_alinéa_1_l521_3
```

#### [Article R521-2|LEGIARTI000006750608]

Dans les situations visées au deuxième alinéa de l'article L. 521-2 ,
l'allocataire est celui des deux parents qu'ils désignent d'un commun accord.
A défaut d'accord sur la désignation d'un allocataire unique, chacun des deux
parents peut se voir reconnaître la qualité d'allocataire :

1° Lorsque les deux parents en ont fait la demande conjointe ;

2° Lorsque les deux parents n'ont ni désigné un allocataire unique, ni fait
une demande conjointe de partage.

```catala
# Le fait que l'allocataire dans le cas d'un commun accord soit celui des
# deux parents désigné par l'accord est déjà modélisé dans L512-2.
```

Lorsque les parents ont désigné un allocataire unique ou fait une demande
conjointe de partage, ils ne peuvent remettre en cause les modalités ainsi
choisies qu'au bout d'un an, sauf modification des modalités de résidence du
ou des enfants.

```catala
# On ne modélise pas le délai minimal entre changement de situation dans
# ce programme.
```

#### [Article R521-3|LEGIARTI000006750610]

Sous réserve de l'article R. 521-4, dans les situations visées aux 1° et 2° de
l'article R. 521-2 , la prestation due à chacun des parents est égale au
montant des allocations familiales dues pour le total des enfants à charge,
multiplié par un coefficient résultant du rapport entre le nombre moyen
d'enfants et le nombre total d'enfants.

```catala
champ d'application AllocationsFamiliales :
  définition montant_avec_garde_alternée_base égal à
    montant_initial_base *€ rapport_enfants_total_moyen

  définition rapport_enfants_total_moyen égal à
    si nombre_total_enfants = 0,0 alors 0,0 sinon
      (nombre_moyen_enfants /. nombre_total_enfants)
```

Le nombre moyen d'enfants, pour chaque foyer, est obtenu en faisant la somme
du nombre d'enfants à charge dans les conditions suivantes :

1° Chaque enfant en résidence alternée compte pour 0,5 ;

2° Les autres enfants à charge comptent pour 1.

```catala
champ d'application AllocationsFamiliales :
  définition nombre_moyen_enfants égal à
    somme décimal pour enfant dans
      enfants_à_charge_droit_ouvert_prestation_familiale
    de (
      selon (prise_en_compte de enfant) sous forme
      -- Complète : 1,0
      -- Partagée : 0,5
      # Pas directement dans cet arricle de loi, mais concerne le
      # cas où l'enfant est pris en charge par les services sociaux et
      # la part de l'allocation va également au service social
      -- Zéro: 0,0
    )
```

Le nombre total d'enfants, pour chaque foyer, est obtenu en faisant la somme du
ou des enfants en résidence alternée et, le cas échéant, du ou des autres
enfants à charge.

```catala
champ d'application AllocationsFamiliales :
  définition nombre_total_enfants égal à
    entier_vers_décimal de (nombre de
      enfants_à_charge_droit_ouvert_prestation_familiale)
```

#### [Article R521-4|LEGIARTI000006750613]

Pour l'ouverture du droit à la majoration prévue à l'article L. 521-3 , le
nombre d'enfants à charge est évalué dans les conditions prévues au premier
alinéa de l'article R. 521-3.

```catala
# enfants_à_charge ne contient que des enfants à charge donc rien à formaliser
# ici
```

Lorsque le ou les enfants ouvrant droit à ladite majoration sont en résidence
alternée, le montant servi au titre de cette majoration est réduit de moitié.

```catala
champ d'application AllocationsFamiliales :
  définition
    montant_avec_garde_alternée_majoration de enfant
  égal à montant_initial_majoration de enfant *€ (
    selon (prise_en_compte de enfant) sous forme
    -- Complète : 100%
    -- Partagée : 50%
    # Pas directement dans cet arricle de loi, mais concerne le
    # cas où l'enfant est pris en charge par les services sociaux et
    # la part de l'allocation va également au service social
    -- Zéro: 0%
  )
```

#### Livre 7 : Régimes divers - Dispositions diverses

##### Titre 5 : Départements d'outre-mer

###### Chapitre 5 : Prestations familiales et prestations assimilées

#### [Article R755-0-2|LEGIARTI000006752633]

Le plafond de rémunération des enfants à charge mentionnés à l'article
L. 512-3 est égal, pour un mois, à 55 % du salaire minimum de croissance en
vigueur dans chacun des départements mentionnés à l'article L. 751-1 ,
multiplié par 169.

```catala
champ d'application PrestationsFamiliales :
  exception
  définition plafond_l512_3_2
  sous condition régime_outre_mer_l751_1
  conséquence égal à
    (smic.brut_horaire *€ 55%) *€ 169,0
```
