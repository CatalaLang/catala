Testing adequacy of the scope calling convention with various types of
parameters (reentrant, functions ...) ; and different calls (through subscopes
or direct scope calls). The main part of the test is in `mod_use_context`.

> Module Mod_def_context

```catala-metadata
declaration scope S:
  context output ci content integer
  context output cm content money
  context output cfun1 content decimal depends on x content integer
  input output cfun2 content decimal depends on x content integer
```

```catala
scope S:
  definition ci equals 0
  definition cm equals $0
  definition cfun1 of x equals x / 2
```

Now testing direct calls within the same module

```catala-metadata
declaration third content decimal
  depends on x content integer
  equals x / 3

declaration quarter content decimal
  depends on x content integer
  equals x / 4
```

```catala
#[test] declaration scope Stest:
  output o1 content S
  output o2 content S
  output x11 content decimal
  output x12 content decimal
  output x21 content decimal
  output x22 content decimal

scope Stest:
  definition o1 equals
      output of S with { -- cfun2: quarter }
  definition o2 equals
      output of S with {
        -- ci: 1
        -- cm: $1
        -- cfun1: third
        -- cfun2: quarter
      }
  definition x11 equals o1.cfun1 of 24
  definition x12 equals o1.cfun2 of 24
  definition x21 equals o2.cfun1 of 24
  definition x22 equals o2.cfun2 of 24
```



```catala-test-cli
$ catala Typecheck --check-invariants
┌─[RESULT]─
│ All invariant checks passed
└─
┌─[RESULT]─
│ Typechecking successful!
└─
```

```catala-test-cli
$ catala test-scope Stest
┌─[RESULT]─ Stest ─
│ o1 = S { -- ci: 0 -- cm: $0.00 -- cfun1: <function> -- cfun2: <function> }
│ o2 = S { -- ci: 1 -- cm: $1.00 -- cfun1: <function> -- cfun2: <function> }
│ x11 = 12.0
│ x12 = 6.0
│ x21 = 8.0
│ x22 = 6.0
└─
```

### Testing subscopes (with and without context override)

```catala
#[test] declaration scope TestSubDefault:
  sub scope S
  output ci content integer
  output cm content money
  output x11 content decimal
  output x12 content decimal

scope TestSubDefault:
  definition sub.cfun2 of x equals quarter of x
  definition ci equals sub.ci
  definition cm equals sub.cm
  definition x11 equals sub.cfun1 of 24
  definition x12 equals sub.cfun2 of 24
```

```catala-test-cli
$ catala dcalc -s TestSubDefault
let scope TestSubDefault
  (TestSubDefault_in: TestSubDefault_in)
  : TestSubDefault
  =
  let set sub : S =
    let result : S =
      S
        { S_in
          ci_in = ∅;
          cm_in = ∅;
          cfun1_in = λ (x: integer) → ∅;
          cfun2_in =
            λ (x: integer) →
            error_empty ⟨ ⟨true ⊢ ⟨quarter x⟩⟩ | false ⊢ ∅ ⟩;
        }
    in
    let result1 : S =
      { S
        ci = result.ci;
        cm = result.cm;
        cfun1 = λ (param0: integer) → result.cfun1 param0;
        cfun2 = λ (param0: integer) → result.cfun2 param0;
      }
    in
    if true then result1 else result1
  in
  let set x12 : decimal =
    error_empty ⟨ ⟨true ⊢ ⟨sub.cfun2 24⟩⟩ | false ⊢ ∅ ⟩
  in
  let set x11 : decimal =
    error_empty ⟨ ⟨true ⊢ ⟨sub.cfun1 24⟩⟩ | false ⊢ ∅ ⟩
  in
  let set cm : money = error_empty ⟨ ⟨true ⊢ ⟨sub.cm⟩⟩ | false ⊢ ∅ ⟩ in
  let set ci : integer = error_empty ⟨ ⟨true ⊢ ⟨sub.ci⟩⟩ | false ⊢ ∅ ⟩ in
  return { TestSubDefault ci = ci; cm = cm; x11 = x11; x12 = x12; }
```

```catala
#[test] declaration scope TestSubOverride:
  sub scope S
  output ci content integer
  output cm content money
  output x21 content decimal
  output x22 content decimal

scope TestSubOverride:
  definition sub.ci equals 1
  definition sub.cm equals $1
  definition sub.cfun1 of x equals third of x
  definition sub.cfun2 of x equals quarter of x
  definition ci equals sub.ci
  definition cm equals sub.cm
  definition x21 equals sub.cfun1 of 24
  definition x22 equals sub.cfun2 of 24
```

```catala-test-cli
$ catala test-scope TestSubOverride
┌─[RESULT]─ TestSubOverride ─
│ ci = 1
│ cm = $1.00
│ x21 = 8.0
│ x22 = 6.0
└─
```
